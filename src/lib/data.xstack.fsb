  \ data.xstack.fsb
  \
  \ This file is part of Solo Forth
  \ http://programandala.net/en.program.solo_forth.html

  \ Last modified: 201701210011

  \ -----------------------------------------------------------
  \ Description

  \ `xstack`, an implementation of named
  \ extra stacks.

  \ -----------------------------------------------------------
  \ Author

  \ Marcos Cruz (programandala.net), 2015, 2016, 2017.
  \
  \ Code adapted from Galope (xstack module).

  \ -----------------------------------------------------------
  \ License

  \ You may do whatever you want with this work, so long as you
  \ retain every copyright, credit and authorship notice, and
  \ this license.  There is no warranty.

  \ -----------------------------------------------------------
  \ Latest changes

  \ 2016-10-14: Rename `xstack` to `allocate-xstack` and write
  \ `allot-xstack`. Move the common code to its own block.
  \
  \ 2016-11-26: Improve `allot-xstack`.
  \
  \ 2016-12-30: Compact the code, saving one block.
  \
  \ 2017-01-18: Remove `exit` at the end of conditional
  \ interpretation.
  \
  \ 2017-01-21: Replace `xdepth.` with `.depth`. Rename `.x` to
  \ `.xs`.

( xsize xp xp0 xp@ xp! xp+! xclear set-xstack )

  \ Core manipulation of xstacks.

need value

0 value xsize  0 value xp  0 value xp0
  \ Values of the current xstack:
  \ xsize = size in address units (constant)
  \ xp = address of the xstack pointer (variable)
  \ xp0 = initial value of the xstack pointer (constant)

: xp@  ( -- a )  xp @  ;

: xp!  ( a -- )  xp !  ;

: xp+!  ( n -- )  xp +!  ;

: xclear  ( -- )  xp0 xp!  ;

: set-xstack  ( a -- )
  dup @ to xp0  cell+ dup to xp  cell+ @ to xsize  ;
  \ Set the xstack at _a_ the current one.

( xfree allocate-xstack allot-xstack )

  \ Creation of xstacks in the heap: `allocate-xstack`, `xfree`.

[unneeded] xfree ?\ : xfree  ( -- )  xp0 free throw  ;

need ?(  [unneeded] allocate-xstack ?(

need allocate  need free need set-xstack  need xp0

: allocate-xstack  ( n "name" -- )
  create  cells dup allocate throw  cell - dup
    , , ,
    \ +0 = xp0
    \ +2 = xp
    \ +4 = xsize
  does> ( -- ) ( pfa ) set-xstack  ; ?)
  \ Create, in the heap, a xstack _name_ of _n_ cells which,
  \ when executed, will be the current one.

[unneeded] allot-xstack ?(  need set-xstack

  \ Creation of xstacks in data space: `allot-xstack`.

: allot-xstack  ( n "name" -- )
  \ Create a new xstack of _n_ cells.
  create  cells dup here cell+ cell+ dup
    , , , allot
    \ +0 = xp0
    \ +2 = xp
    \ +4 = xsize
    \ +6 = stack space
  does> ( -- )  ( pfa ) set-xstack  ; ?)
  \ Create, in data space, a xstack _name_ of _n_ cells which,
  \ when executed, will be the current one.

( >x x@ xdrop x> xdup xpick )

  \ xstack single-number operations

: >x  ( x -- ) ( X: -- x )  cell xp+!  xp@ !   ;

: x@  ( -- x ) ( X: x -- x )  xp@ @  ;

: xdrop  ( X: x -- )  [ cell negate ] literal xp+!  ;

: x>  ( -- x ) ( X: x -- )  x@ xdrop  ;

: xdup  ( X: x -- x x )  x@ >x  ;

: xpick  ( n -- x'n ) ( X: x'n ... x'0 -- x'n ... x'0 )
  xp@ swap cells - @  ;

: xover  ( X: x1 x2 -- x1 x2 x1 )  1 xpick >x  ;

( 2x@ 2>x 2x> 2xdrop 2xdup )

  \ xstack double-number operations

need x@  need >x  need xpick  need xover

: 2x@  ( -- x1 x2 ) ( X: x1 x2 -- x1 x2 )  x@ 1 xpick swap  ;

: 2>x  ( x1 x2 -- ) ( X: -- x1 x2 )  swap >x >x  ;

: 2x>  ( -- x1 x2 ) ( X: x1 x2 -- )  x> x> swap  ;

: 2xdrop  ( X: x1 x2 -- )  [ -2 cells ] literal xp+!  ;

: 2xdup  ( X: x1 x2 -- x1 x2 x1 x2 )  xover xover  ;

( xlen xdepth .xs )

  \ xstack tools

need .depth

: xlen  ( -- n )  xp@ xp0 -  ;
  \ Length of the current xstack, in address units.

: xdepth  ( -- n )  xlen cell /  ;
  \ Depth of the current xstack.

: (.xs)  ( -- )  xp0 cell+ xlen bounds ?do  i @ . cell +loop  ;
  \ Display a list of the items in the xstack; TOS is the
  \ right-most item.

: .xs  ( -- )  xdepth dup .depth if  (.xs)  then  ;
  \ Display the number of items on the current xstack, followed
  \ by a list of the items, if any; TOS is the right-most item.

  \ vim: filetype=soloforth
