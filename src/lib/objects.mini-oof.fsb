  \ objects.mini-oof.fsb
  \
  \ This file is part of Solo Forth
  \ http://programandala.net/en.program.solo_forth.html

  \ Last modified: 201611191221

  \ -----------------------------------------------------------
  \ Description

  \ Words related to word lists.

  \ -----------------------------------------------------------
  \ Authors

  \ Bernd Paysan, 1998.
  \
  \ Integrated into Solo Forth by Marcos Cruz (programandala.net),
  \ 2016.

  \ -----------------------------------------------------------
  \ License

  \ You may do whatever you want with this work, so long as you
  \ retain every copyright, credit and authorship notice, and
  \ this license.  There is no warranty.

  \ -----------------------------------------------------------
  \ History

  \ 2016-11-19: Copy the original code.

( mini-oof )

: method ( m v "name" -- m' v )
  create  over , swap cell+ swap
  does> ( ... o -- ... ) @ over @ + @ execute ;
: var ( m v size "name" -- m v' )
  create  over , +  does> ( o -- addr ) @ + ;
: class ( class -- class methods vars ) dup 2@ ;
: end-class  ( class methods vars "name" -- )
  create  here >r , dup , 2 cells ?do ['] noop , 1 cells +loop
  cell+ dup cell+ r> rot @ 2 cells /string move ;
: defines ( xt class "name" -- ) ' >body @ + ! ;
: new ( class -- o )  here over @ allot swap over ! ;
: :: ( class "name" -- ) ' >body @ + @ compile, ;
create object  1 cells , 2 cells ,
