  \ graphics.screen_filters.fsb
  \
  \ This file is part of Solo Forth
  \ http://programandala.net/en.program.solo_forth.html

  \ Last modified: 201701051642

  \ -----------------------------------------------------------
  \ Description

  \ Words that make filter effects to the
  \ whole screen.

  \ -----------------------------------------------------------
  \ Author

  \ Marcos Cruz (programandala.net), 2015, 2016.

  \ -----------------------------------------------------------
  \ License

  \ You may do whatever you want with this work, so long as you
  \ retain every copyright, credit and authorship notice, and
  \ this license.  There is no warranty.

  \ -----------------------------------------------------------
  \ Latest changes

  \ 2016-12-20: Rename `jpnext` to `jpnext,` after the change
  \ in the kernel.
  \
  \ 2016-12-30: Compact the code, saving one block.
  \
  \ 2017-01-02: Convert `inverted` from `z80-asm` to `z80-asm,`
  \ and fix it.
  \
  \ 2017-01-05: Convert `water` from `z80-asm` to `z80-asm,`.
  \ Improve documentation.
  \
  \ 2017-01-05: Update `need z80-asm,` to `need assembler`.

( fade )

need assembler

code fade  ( -- )
  b push,
  8 b ld#,
  \ do:
  rbegin  5AFF h ldp#, halt, halt,
    rbegin
      m a ld, a d ld, 07 and#, nz? rif  a dec,  rthen
      a e ld, a d ld, 38 and#, nz? rif  8 sub#,  rthen
      e or, d xor, 3F and#, d xor,
      a m ld, h decp, h a ld,
      58 cp#,
    c? runtil
  rstep
  b pop, jpnext,
  end-code
  
  \ doc{
  \
  \ fade  ( -- )
  \
  \ Do a screen fade to black, by decrementing the values of
  \ paper and ink in a loop.
  \
  \ }doc

  \ Credit:
  \
  \ Code adapted from a routine written by Pablo Ariza,
  \ published on Microhobby Especial, issue 7 (1987-12), page
  \ 46: <http://microhobby.org/mhes7.htm>.

( inverted water )

need ?(  [unneeded] inverted ?(  need assembler

code inverted  ( -- )

  4000 h ldp#,
  rbegin   m a ld, cpl, a m ld, h incp,
           h a ld, 58 cp#,  z? runtil
  jpnext,  end-code  exit ?)
  
  \ Credit:
  \
  \ Code adapted from a routine written by Javier Granadino,
  \ published on Microhobby, issue 133 (1987-06), page 7:
  \ http://microhobby.org/numero133.htm
  \ http://microhobby.speccy.cz/mhf/133/MH133_07.jpg
  
  \ doc{
  \
  \ inverted  ( -- )
  \
  \ Invert the pixels of the whole screen.
  \
  \ }doc

[unneeded] water ?(  need assembler

code water  ( -- )

  b push, 20 b ld#,
  rbegin  57FF h ldp#,
          rbegin   m rrc, h decp, h 6 bit,  z? runtil
  rstep   b pop, jpnext,  end-code ?)

  \ Credit:
  \
  \ Code adapted from a routine written by Juan Jos√© Rivas,
  \ published on Microhobby, issue 150 (1987-06), page 9:
  \ http://microhobby.org/numero150.htm
  \ http://microhobby.speccy.cz/mhf/150/MH133_09.jpg
  
  \ doc{
  \
  \ water  ( -- )
  \
  \ Modify the screen bitmap with a water effect. At the end
  \ the original image is  restored.
  \
  \ }doc

  \ vim: filetype=soloforth
