  \ graphics.scroll.fsb
  \
  \ This file is part of Solo Forth
  \ http://programandala.net/en.program.solo_forth.html

  \ Last modified: 201701050008

  \ -----------------------------------------------------------
  \ Description

  \ Words to scroll the screen.

  \ -----------------------------------------------------------
  \ Author

  \ Marcos Cruz (programandala.net), 2015, 2016.

  \ -----------------------------------------------------------
  \ License

  \ You may do whatever you want with this work, so long as you
  \ retain every copyright, credit and authorship notice, and
  \ this license.  There is no warranty.

  \ -----------------------------------------------------------
  \ Latest changes

  \ 2016-12-20: Rename `jpnext` to `jpnext,` after the change
  \ in the kernel.
  \
  \ 2017-01-04: Convert `scroll-1px-up` and `(scroll-1px-up)`
  \ from `z80-asm` to `z80-asm,`. Improve documentation. Fix:
  \ add missing `ret,` to `(scroll-1px-up)`.

( scroll-1px-right )

need z80-asm,

code scroll-1px-right  ( -- )

  b push,

  4000 h ldp#,  \ screen bitmap address
  C0 c ld#,  \ pixel rows
  rbegin
    m srl, h incp,  \ first char column
    1F b ld#,  \ remaining columns
    rbegin  m rr, h incp,  rstep
    c dec,
  z? runtil

  b pop, jpnext,

  end-code

  \ Credit:
  \
  \ Code adapted from a routine written by Antonio Adolfo Sanz,
  \ published on Microhobby, issue 197 (1990-03), page 24:
  \ http://microhobby.org/numero197.htm
  \ http://microhobby.speccy.cz/mhf/197/MH197_24.jpg

  \ doc{
  \
  \ scroll-1px-right  ( -- )
  \
  \ Scroll the whole screen one pixel up.
  \
  \ }doc

( scroll-1px-up )

need z80-asm,

create (scroll-1px-up)  ( -- a )

  asm

  4000 h ldp#, BF b ld#,

  rbegin
    b push, h d ldp, h inc, h a ld, F8 and#, h cp,
    z? rif  08 b ld#, b sub,
            rra, rra, rra, a h ld,
            0020 b ldp#, b addp, h a ld,
            rla, rla, rla, a h ld,
    rthen   h push, 0020 b ldp#, ldir, h pop, b pop,
  rstep  ret,

  end-asm

  \ Credit:
  \
  \ Code adapted from a routine written by Iván Sansa,
  \ published on Microhobby, issue 122 (1987-03), page 7:
  \ http://microhobby.org/numero122.htm
  \ http://microhobby.speccy.cz/mhf/122/MH122_07.jpg
  
  \ doc{
  \
  \ (scroll-1px-up)  ( -- a )
  \
  \ Return the address _a_ of a Z80 routine that 
  \ scrolls the whole screen one pixel up.
  \
  \ }doc

code scroll-1px-up  ( -- )
  b push, (scroll-1px-up) call, b pop, jpnext,  end-code

  \ doc{
  \
  \ scroll-1px-up  ( -- )
  \
  \ Scroll the whole screen one pixel up.
  \
  \ }doc

  \ vim: filetype=soloforth
