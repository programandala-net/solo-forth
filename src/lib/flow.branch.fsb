  \ flow.branch.fsb
  \
  \ This file is part of Solo Forth
  \ http://programandala.net/en.program.solo_forth.html

  \ Last modified: 201611241513

  \ -----------------------------------------------------------
  \ Description

  \ Alternative branch words.

  \ -----------------------------------------------------------
  \ Author

  \ Marcos Cruz (programandala.net), 2015, 2016.

  \ -----------------------------------------------------------
  \ License

  \ You may do whatever you want with this work, so long as you
  \ retain every copyright, credit and authorship notice, and
  \ this license.  There is no warranty.

  \ -----------------------------------------------------------
  \ Latest changes

  \ 2016-11-24: Move `+branch` from
  \ <flow.conditionals.negative.fsb>.  Add `-branch`. Improve
  \ documentation.

  \ XXX TODO -- Compilation stack comments.

( -branch +branch )

need ?(

[unneeded] -branch ?(
code -branch  ( n -- )
  E1 c,  CB c, 7C c,  C2 c, ' branch ,  03 c, 03 c,
    \ pop hl
    \ bit 7,h           ; negative?
    \ jp nz,branch_xt   ; branch if negative
    \ inc bc
    \ inc bc            ; skip the inline branch address
  jpnext  end-code  exit ?)

  \ doc{
  \
  \ -branch  ( n -- )
  \
  \ A run-time procedure to branch conditionally. If  _n_ is
  \ negative, the following in-line address is copied to IP to
  \ branch forward or backward.
  \
  \ This word is compiled by `+if` and `+until`.
  \
  \ See: `+if`, `+while`, `+until`.
  \ 
  \ }doc

[unneeded] +branch ?(
code +branch  ( n -- )
  E1 c,  CB c, 7C c,  CA c, ' branch ,  03 c, 03 c,
    \ pop hl
    \ bit 7,h         ; positive?
    \ jp z,branch_xt  ; branch if positive
    \ inc bc
    \ inc bc          ; skip the inline branch address
  jpnext  end-code  exit ?)

  \ doc{
  \
  \ +branch  ( n -- )
  \
  \ A run-time procedure to branch conditionally. If  _n_ is
  \ positive, the following in-line address is copied to IP to
  \ branch forward or backward.
  \
  \ This word is compiled by `-if` and `-until`.
  \
  \ See: `-if`, `-while`, `-until`.
  \ 
  \ }doc

  \ vim: filetype=soloforth

