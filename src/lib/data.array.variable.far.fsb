  \ data.array.variable.far.fsb
  \
  \ This file is part of Solo Forth
  \ http://programandala.net/en.program.solo_forth.html

  \ Last modified: 201701182337

  \ -----------------------------------------------------------
  \ Description

  \ Words to create and manage 1-dimension single-cell,
  \ double-cell and character variables arrays, which behave
  \ like `variable`, in far memory.

  \ Usage example of a single-cell variables array:

    \ 4 faravariable bar
    \ 10 0 bar far!  20 1 bar far!  30 2 bar far!  40 3 bar far!
    \ 3 bar far@ .
    \ 0 bar far@ .
    \ 123 3 bar !
    \ 3 bar far@ .
    \ 1 3 bar +!
    \ 3 bar far@ .

  \ -----------------------------------------------------------
  \ Author

  \ Marcos Cruz (programandala.net), 2017.

  \ -----------------------------------------------------------
  \ License

  \ You may do whatever you want with this work, so long as you
  \ retain every copyright, credit and authorship notice, and
  \ this license.  There is no warranty.

  \ -----------------------------------------------------------
  \ History

  \ 2017-01-18: Start, based on the module
  \ <data.array.variable.fsb>.

( faravariable far2avariable farcavariable )

need ?(

[unneeded] faravariable ?(  need farallot  need array>

: faravariable  ( n "name" -- )
  create  hp@ , cells farallot
  does>  ( n -- a ) ( n pfa ) @ array>  ; ?)

  \ doc{
  \
  \ faravariable  ( n "name" -- )
  \
  \ Create, in far memory, a 1-dimension single-cell variables
  \ array _name_ with _n_ elements and the execution semantics
  \ defined below.
  \
  \ `name` execution:
  \
  \ name  ( n -- a )
  \
  \ Return far-memory address _a_ of element _n_.
  \
  \ See: `far2avariable`, `farcavariable`, `avariable`.
  \
  \ }doc

[unneeded] far2avariable ?(  need farallot need 2array>

: far2avariable  ( n "name" -- )
  create  hp@ , [ 2 cells ] cliteral * farallot
  does>  ( n -- a ) ( n pfa ) @ 2array>  ; ?)

  \ doc{
  \
  \ 2avariable  ( n "name" -- )
  \
  \ Create, in far memory, a 1-dimension double-cell variables
  \ array _name_ with _n_ elements and the execution semantics
  \ defined below.
  \
  \ `name` execution:
  \
  \ name  ( n -- a )
  \
  \ Return far-memory address _a_ of element _n_.
  \
  \ See: `faravariable`, `farcvariable`, `2avariable`.
  \
  \ }doc

[unneeded] cavariable ?(  need farallot

: farcavariable  ( n "name" -- )
  create  hp@ , farallot
  does>  ( n -- ca ) ( n pfa ) @ +  ; ?)

  \ doc{
  \
  \ farcavariable  ( n "name" -- )
  \
  \ Create, in far memory, a 1-dimension character variables
  \ array _name_ with _n_ elements and the execution semantics
  \ defined below.
  \
  \ `name` execution:
  \
  \ name  ( n -- ca )
  \
  \ Return far-memory address _ca_ of element _n_.
  \
  \ See: `faravariable`, `far2variable`, `cavariable`.
  \
  \ }doc

  \ vim: filetype=soloforth


