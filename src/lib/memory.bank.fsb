  \ memory.bank.fsb
  \
  \ This file is part of Solo Forth
  \ http://programandala.net/en.program.solo_forth.html

  \ Last modified: 201610241915

  \ -----------------------------------------------------------
  \ Description

  \ Words related to memory banks.

  \ -----------------------------------------------------------
  \ Author

  \ Marcos Cruz (programandala.net), 2015, 2016.

  \ -----------------------------------------------------------
  \ License

  \ You may do whatever you want with this work, so long as you
  \ retain every copyright, credit and authorship notice, and
  \ this license.  There is no warranty.

  \ -----------------------------------------------------------
  \ History

  \ 2016-04-17: Move `!s` and `!cs` from the kernel.
  \
  \ 2016-04-24: Move `get-default-bank` and `set-default-bank`
  \ from the kernel.
  \
  \ 2016-10-24: Add `extra-memory` and related words to manage
  \ a virtual 64-KiB space using 4 memory banks.

( !s c!s )

: !s  ( x a -- )  system-bank ! default-bank  ;

  \ doc{
  \
  \ !s  ( x a -- )
  \
  \ Store _x_ into address _a_ of the system bank.
  \
  \ }doc

: c!s  ( c ca -- )  system-bank c! default-bank  ;

  \ doc{
  \
  \ c!s  ( c ca -- )
  \
  \ Store _c_ into address _ca_ of the system bank.
  \
  \ }doc

( get-default-bank set-default-bank )


: get-default-bank  ( -- +n )  default-bank# c@  ;

  \ doc{
  \
  \ get-default-bank  ( -- +n )
  \
  \ Get the current default bank _+n_ (0..7) paged in at
  \ $C000..$FFFF.
  \
  \ }doc

: set-default-bank  ( +n -- )  default-bank# c!  ;

  \ doc{
  \
  \ set-default-bank  ( +n -- )
  \
  \ Set _+n_ (0..7) as the default memory bank paged in at
  \ $C000..$FFFF.
  \
  \ }doc

( extra-memory a>e c!e c@e !e !eb @e @eb )

need u>ud  need join  need split

create extra-memory  ( -- ca )  1 c, 3 c, 4 c, 7 c,

  \ doc{
  \
  \ extra-memory  ( -- ca )
  \
  \ Address of an array of four bytes. It holds the memory
  \ banks used as extra memory, which is a virtual 64-KiB
  \ continuous space: The first bank in the array is used for
  \ addresses $0000..$3FFF, the second one for $4000..$7999,
  \ the third one for $8000..$BFFF and the fourth one for
  \ $C000..$FFFF.
  \
  \ }doc

  \ XXX TODO -- These banks are for ZX Spectrum 128K and +2.
  \ They are different for +2A, +3 and +3e.

: a>e  ( a1 -- a2 )
  u>ud $4000 um/mod  extra-memory + c@ bank  $C000 +  ;

  \ doc{
  \
  \ a>e  ( a1 -- a2 )
  \
  \ Convert an extra memory address _a1_ ($0000..$FFFF) to its
  \ equivalent address _a2_ ($C000..$FFFF) in the correspondent
  \ memory bank, which is paged in.
  \
  \ }doc

: c!e  ( c a -- )  a>e c! default-bank  ;

  \ doc{
  \
  \ c!e  ( c a -- )
  \
  \ Store _c_ into extra-memory address _a_.
  \
  \ }doc

: c@e  ( a -- c )  a>e c@ default-bank  ;

  \ doc{
  \
  \ c@e ( a -- c )
  \
  \ Fetch _c_ from extra-memory address _a_.
  \
  \ }doc

: !e  ( x a -- )  >r split r@ 1+ a>e c! r> c!e  ;

  \ XXX OLD -- faster but larger version:
  \ : !e  ( x a -- )
  \   >r split r@ 1+ a>e c! r> a>e c! default-bank  ;

  \ doc{
  \
  \ !e  ( x a -- )
  \
  \ Store _x_ into extra-memory address _a_.
  \
  \ See: `!eb`.
  \
  \ }doc

: !eb  ( x a -- )  a>e ! default-bank   ;

  \ doc{
  \
  \ !eb ( a -- x )
  \
  \ Store _x_ into extra-memory address _a_, provided _a_ and
  \ _a+1_ belong to the same bank.  In other words, _a_ can not
  \ be the last address of a 16-KiB section ($3FFF, $7FFF,
  \ $BFFF or $FFFF), else the high byte of _x_ will be "stored"
  \ into ROM address $0000.
  \
  \ See: `!e`.
  \
  \ }doc

: @e  ( a -- x )  dup 1+ a>e c@ >r c@e r> join   ;

  \ XXX OLD -- faster but larger version:
  \ : @e  ( a -- x )
  \   dup 1+ a>e c@ >r a>e c@ r> join default-bank  ;

  \ doc{
  \
  \ @e ( a -- x )
  \
  \ Fetch _x_ from extra-memory address _a_.
  \
  \ See: `@eb`.
  \
  \ }doc

: @eb  ( a -- x )  a>e @ default-bank   ;

  \ doc{
  \
  \ @eb ( a -- x )
  \
  \ Fetch _x_ from extra-memory address _a_, provided _a_ and
  \ _a+1_ belong to the same bank. In other words, _a_ can not
  \ be the last address of a 16-KiB section ($3FFF, $7FFF,
  \ $BFFF or $FFFF), else the high byte of _x_ will be fetched
  \ from ROM address $0000.
  \
  \ See: `@e`.
  \
  \ }doc

  \ vim: filetype=soloforth
