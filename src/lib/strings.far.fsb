  \ strings.far.fsb
  \
  \ This file is part of Solo Forth
  \ http://programandala.net/en.program.solo_forth.html

  \ Last modified: 201701191512

  \ -----------------------------------------------------------
  \ Description

  \ Words to manage far-memory strings.

  \ -----------------------------------------------------------
  \ Author

  \ Marcos Cruz (programandala.net), 2015, 2016, 2017.

  \ -----------------------------------------------------------
  \ License

  \ You may do whatever you want with this work, so long as you
  \ retain every copyright, credit and authorship notice, and
  \ this license.  There is no warranty.

  \ -----------------------------------------------------------
  \ Latest changes

  \ 2017-01-10: Move all the far-memory string words from
  \ <strings.MISC.fsb>.  Simplify the string arrays: remove the
  \ variants that don't leave the count on the stack and remove
  \ the slash from the names of the others.
  \
  \ 2017-01-18: Remove `exit` at the end of conditional
  \ interpretation.
  \
  \ 2017-01-19: Remove remaining `exit` at the end of
  \ conditional interpretation.

( far," fars, farsconstant save-farstring )

[unneeded] far,"

?\ need fars,  : far,"  ( -- )  '"' parse fars,  ;

  \ doc{
  \
  \ far,"  ( "ccc<quote>" -- )
  \
  \ Parse "ccc" delimited by a double-quote and compile the
  \ string in far memory.
  \
  \ }doc

need ?(

[unneeded] fars, ?(  need farplace  need farallot

: fars,  ( ca len -- )  tuck hp@ farplace 1+ farallot  ; ?)

  \ doc{
  \
  \ fars,  ( ca len -- )
  \
  \ Compile a string in far memory.
  \
  \
  \ }doc

[unneeded] farsconstant ?(  need fars,

: farsconstant  ( ca len "name" -- )
  hp@ >r fars, r> farcount 2constant  ; ?)

  \ doc{
  \
  \ farsconstant  ( ca len "name" -- )
  \
  \ Create a string constant _name_ in far memory with value
  \ _ca len_.
  \
  \ When _name_ is executed, it returns the string _ca len_ in
  \ far memory as _ca2 len_.
  \
  \ }doc

[unneeded] save-farstring ?(  need cmove<far

: save-farstring  ( ca1 len1 -- ca2 len1 )
  dup allocate-string swap 2dup 2>r cmove<far 2r>  ; ?)

  \ doc{
  \
  \ save-farstring  ( ca1 len1 -- ca2 len1 )
  \
  \ Save the string _ca1 len1_, which is in far memory, in the
  \ circular string buffer and return it at its new address as
  \ _ca2 len1_.
  \
  \ See: `save-string`, `csb0`.
  \
  \ }doc

( farsconstants, farsconstants far>sconstants )

need ?(

[unneeded] farsconstants, ?(  need far,

: farsconstants,  ( 0 ca[n]..ca[1] "name" -- n )
  create  hp@ ,
  0 begin  swap ?dup  while  far, 1+  repeat  ; ?)

  \ doc{
  \
  \ farsconstants,  ( 0 ca[n]..ca[1] "name" -- n )
  \
  \ Create a table of string constants _name_ in far memory,
  \ using counted strings _ca[n]..ca[1]_, being _0_ a mark for
  \ the last string on the stack, and return the number _n_ of
  \ compiled strings.
  \
  \ When _name_ is executed, it returns an address that holds
  \ the address of the table in far memory.
  \
  \ This word is a common factor of `farsconstants` and
  \ `far>sconstants`.
  \
  \ See: `farsconstants`, `far>sconstants`.
  \
  \ }doc

[unneeded] farsconstants ?(  need farsconstants,  need array>

: farsconstants  ( 0 ca[n]..ca[1] "name" -- n )
  farsconstants,  does>  ( n -- ca len )
  ( n pfa ) @ array> far@ farcount  ; ?)

  \ doc{
  \
  \ farsconstants  ( 0 ca[n]..ca[1] "name" -- )
  \
  \ Create a table of string constants _name_ in far memory,
  \ using counted strings _ca[n]..ca[1]_, being _0_ a mark for
  \ the last string on the stack, and return the number _n_ of
  \ compiled strings.
  \
  \ When _name_ is executed, it converts the index on the stack
  \ (0.._n-1_) to the correspondent string _ca len_ in far
  \ memory.
  \
  \ Usage example:

  \ ----
  \
  \ 0                  \ end of strings
  \   hp@ far," kvar"  \ string 4
  \   hp@ far," tri"   \ string 3
  \   hp@ far," du"    \ string 2
  \   hp@ far," unu"   \ string 1
  \   hp@ far," nul"   \ string 0
  \ farsconstants digitname  constant digitnames
  \
  \ cr .( There are ) digitnames . .( digit names:)
  \ 0 digitname cr fartype
  \ 1 digitname cr fartype
  \ 2 digitname cr fartype
  \ 3 digitname cr fartype cr
  \ ----

  \ See: `sconstants`.
  \
  \ }doc

[unneeded] far>sconstants ?exit

need farsconstants,  need array>  need save-farstring

: far>sconstants  ( 0 ca[n]..ca[1] "name" -- n )
  farsconstants,  does>  ( n -- ca len )
  ( n pfa ) @ array> far@ farcount save-farstring  ; ?)

  \ doc{
  \
  \ far>sconstants  ( 0 ca[n]..ca[1] "name" -- n )
  \
  \ Create a table of string constants _name_ in far memory,
  \ using counted strings _ca[n]..ca[1]_, being _0_ a mark for
  \ the last string on the stack, and return the number _n_ of
  \ compiled strings.
  \
  \ When _name_ is executed, it converts the index on the stack
  \ (0.._n-1_) to the correspondent string _ca len_ in far
  \ memory, and return a copy in the circular string buffer.
  \
  \ Usage example:

  \ ----
  \
  \ 0                  \ end of strings
  \   hp@ far," kvar"  \ string 4
  \   hp@ far," tri"   \ string 3
  \   hp@ far," du"    \ string 2
  \   hp@ far," unu"   \ string 1
  \   hp@ far," nul"   \ string 0
  \ far>sconstants digitname  constant digitnames
  \
  \ cr .( There are ) digitnames . .( digit names:)
  \ 0 digitname cr type
  \ 1 digitname cr type
  \ 2 digitname cr type
  \ 3 digitname cr type cr
  \ ----

  \ See: `sconstants`, `farsconstants`.
  \
  \ }doc

  \ vim: filetype=soloforth

