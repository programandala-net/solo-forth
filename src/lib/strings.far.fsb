  \ strings.far.fsb
  \
  \ This file is part of Solo Forth
  \ http://programandala.net/en.program.solo_forth.html

  \ Last modified: 201702221416

  \ -----------------------------------------------------------
  \ Description

  \ Words to manage far-memory strings.

  \ -----------------------------------------------------------
  \ Author

  \ Marcos Cruz (programandala.net), 2015, 2016, 2017.

  \ -----------------------------------------------------------
  \ License

  \ You may do whatever you want with this work, so long as you
  \ retain every copyright, credit and authorship notice, and
  \ this license.  There is no warranty.

  \ -----------------------------------------------------------
  \ Latest changes

  \ 2017-01-10: Move all the far-memory string words from
  \ <strings.MISC.fsb>.  Simplify the string arrays: remove the
  \ variants that don't leave the count on the stack and remove
  \ the slash from the names of the others.
  \
  \ 2017-01-18: Remove `exit` at the end of conditional
  \ interpretation.
  \
  \ 2017-01-19: Remove remaining `exit` at the end of
  \ conditional interpretation.
  \
  \ 2017-01-20: Fix documentation.
  \
  \ 2017-02-01: Move `faruppers` from the kernel.
  \
  \ 2017-02-02: Add `far>sconstant` and `farsconstants>`.
  \
  \ 2017-02-03: Fix needing of `far>sconstant`.
  \
  \ 2017-02-17: Update cross references.

( far," fars, farsconstant far>sconstant save-farstring )

[unneeded] far,"

?\ need fars,  : far," ( -- ) '"' parse fars, ;

  \ doc{
  \
  \ far," ( "ccc<quote>" -- )
  \
  \ Parse "ccc" delimited by a double-quote and compile the
  \ string in far memory.
  \
  \ }doc

need ?(

[unneeded] fars, ?( need farplace need farallot

: fars, ( ca len -- ) tuck hp@ farplace 1+ farallot ; ?)

  \ doc{
  \
  \ fars, ( ca len -- )
  \
  \ Compile a string in far memory.
  \
  \
  \ }doc

[unneeded] farsconstant ?( need fars,

: farsconstant ( ca len "name" -- )
  hp@ >r fars, r> farcount 2constant ; ?)

  \ doc{
  \
  \ farsconstant ( ca len "name" -- )
  \
  \ Create a string constant _name_ in far memory with value
  \ _ca len_.
  \
  \ When _name_ is executed, it returns the string _ca len_ in
  \ far memory as _ca2 len_.
  \
  \ See also: `far>sconstant`.
  \
  \ }doc

[unneeded] save-farstring ?( need cmove<far

: save-farstring ( ca1 len1 -- ca2 len1 )
  dup allocate-string swap 2dup 2>r cmove<far 2r> ; ?)

  \ doc{
  \
  \ save-farstring ( ca1 len1 -- ca2 len1 )
  \
  \ Save the string _ca1 len1_, which is in far memory, in the
  \ circular string buffer and return it at its new address as
  \ _ca2 len1_.
  \
  \ See also: `save-string`, `csb0`.
  \
  \ }doc

[unneeded] far>sconstant ?(

need farsconstant need save-farstring

: far>sconstant ( ca len "name" -- )
  farsconstant does> 2@ save-farstring ; ?)

  \ doc{
  \
  \ far>sconstant ( ca len "name" -- )
  \
  \ Create a string constant _name_ in far memory with value
  \ _ca len_.
  \
  \ When _name_ is executed, it returns the string _ca len_ in
  \ the circular string buffer as _ca2 len_.
  \
  \ See also: `farsconstant`.
  \
  \ }doc

( farsconstants, farsconstants> farsconstants far>sconstants )

need ?( [unneeded] farsconstants, ?( need far,

: farsconstants, ( 0 ca[n]..ca[1] "name" -- n )
  create hp@ , 0 begin  swap ?dup while  far, 1+  repeat ; ?)

  \ doc{
  \
  \ farsconstants, ( 0 ca[n]..ca[1] "name" -- n )
  \
  \ Create a table of string constants _name_ in far memory,
  \ using counted strings _ca[n]..ca[1]_, being _0_ a mark for
  \ the last string on the stack, and return the number _n_ of
  \ compiled strings.
  \
  \ When _name_ is executed, it returns an address that holds
  \ the address of the table in far memory.
  \
  \ This word is a common factor of `farsconstants` and
  \ `far>sconstants`.
  \
  \ }doc

[unneeded] farsconstants>
?\ : farsconstants> ( n a -- ca len ) @ array> far@ farcount ;

  \ doc{
  \
  \ farsconstants> ( n a -- ca len )
  \
  \ Return the far-memory string _ca len_ whose address is
  \ stored at the _n_ cell of the table _a_ in data space.
  \
  \ This word is a factor of `farsconstants` and
  \ `far>sconstants`.
  \
  \ }doc

[unneeded] farsconstants ?(

need farsconstants, need array> need farsconstants>

: farsconstants ( 0 ca[n]..ca[1] "name" -- n )
  farsconstants,  does> ( n -- ca len )
  ( n pfa ) farsconstants> ; ?)

  \ doc{
  \
  \ farsconstants ( 0 ca[n]..ca[1] "name" -- )
  \
  \ Create a table of string constants _name_ in far memory,
  \ using counted strings _ca[n]..ca[1]_, being _0_ a mark for
  \ the last string on the stack, and return the number _n_ of
  \ compiled strings.
  \
  \ When _name_ is executed, it converts the index on the stack
  \ (0.._n-1_) to the correspondent string _ca len_ in far
  \ memory.
  \
  \ Usage example:

  \ ----
  \
  \ 0                  \ end of strings
  \   hp@ far," kvar"  \ string 4
  \   hp@ far," tri"   \ string 3
  \   hp@ far," du"    \ string 2
  \   hp@ far," unu"   \ string 1
  \   hp@ far," nul"   \ string 0
  \ farsconstants digitname  constant digitnames
  \
  \ cr .( There are ) digitnames . .( digit names:)
  \ 0 digitname cr fartype
  \ 1 digitname cr fartype
  \ 2 digitname cr fartype
  \ 3 digitname cr fartype cr
  \ ----

  \ See also: `sconstants`, `far>sconstants`.
  \
  \ }doc

[unneeded] far>sconstants ?( need farsconstants,
need array> need farsconstants> need save-farstring

: far>sconstants ( 0 ca[n]..ca[1] "name" -- n )
  farsconstants,  does> ( n -- ca len )
  ( n pfa ) farsconstants> save-farstring ; ?)

  \ doc{
  \
  \ far>sconstants ( 0 ca[n]..ca[1] "name" -- n )
  \
  \ Create a table of string constants _name_ in far memory,
  \ using counted strings _ca[n]..ca[1]_, being _0_ a mark for
  \ the last string on the stack, and return the number _n_ of
  \ compiled strings.
  \
  \ When _name_ is executed, it converts the index on the stack
  \ (0.._n-1_) to the correspondent string _ca len_ in far
  \ memory, and return a copy in the circular string buffer.
  \
  \ Usage example:

  \ ----
  \
  \ 0                  \ end of strings
  \   hp@ far," kvar"  \ string 4
  \   hp@ far," tri"   \ string 3
  \   hp@ far," du"    \ string 2
  \   hp@ far," unu"   \ string 1
  \   hp@ far," nul"   \ string 0
  \ far>sconstants digitname  constant digitnames
  \
  \ cr .( There are ) digitnames . .( digit names:)
  \ 0 digitname cr type
  \ 1 digitname cr type
  \ 2 digitname cr type
  \ 3 digitname cr type cr
  \ ----

  \ See also: `sconstants`, `farsconstants`.
  \
  \ }doc

( faruppers )

need ?(

[unneeded] faruppers ?(

need assembler
need far-hl-routine need upper-routine need ?next-bank-routine

code faruppers ( ca len -- )

  h pop, exsp, far-hl-routine call, d pop,
  rbegin  d a ld, e or,  nz? rwhile
          m a ld, upper-routine call, a m ld, h incp,
          d push, ?next-bank-routine call, d pop, d decp,
  rrepeat ' default-bank jp, end-code ?)

  \   pop hl              ; string length
  \   ex (sp),hl          ; HL = string address
  \   call far_hl_routine ; HL = actual string address
  \                       ; the bank is paged in
  \   pop de              ; string length
  \ far_uppers.do:
  \   ld a,d
  \   or e
  \   jr z,faruppers.end
  \   ld a,(hl)
  \   call upper_routine
  \   ld (hl),a
  \   inc hl
  \   push de
  \   call question_next_bank_routine
  \   pop de
  \   dec de
  \   jp far_uppers.do
  \ far_uppers.end:
  \   jp default_bank_

  \ XXX TODO -- improve to save push/pop

  \ doc{
  \
  \ faruppers ( ca len -- )
  \
  \ Convert string _ca len_, which is stored in far memory, to
  \ uppercase.
  \
  \ See also: `uppers`, `far-banks`.
  \
  \ }doc

  \ vim: filetype=soloforth

