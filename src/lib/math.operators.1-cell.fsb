  \ math.operators.1-cell.fsb
  \
  \ This file is part of Solo Forth
  \ http://programandala.net/en.program.solo_forth.html

  \ Last modified: 201612222116

  \ -----------------------------------------------------------
  \ Description

  \ Single-cell operators.

  \ -----------------------------------------------------------
  \ Author

  \ Marcos Cruz (programandala.net), 2015, 2016.

  \ -----------------------------------------------------------
  \ License

  \ You may do whatever you want with this work, so long as you
  \ retain every copyright, credit and authorship notice, and
  \ this license.  There is no warranty.

  \ -----------------------------------------------------------
  \ Latest changes

  \ 2015: Add `within`, `between`, and common operators.
  \
  \ 2015-08-12: Add `lshift`. Improve `2/`.
  \
  \ 2015-08-14: Add `under+`, in Forth.
  \
  \ 2015-11-01: Add `rshift` and faster `lshift`.
  \
  \ 2015-11-13: Add `sqrt` (version by Wil Baden).
  \
  \ 2015-12-15: Rewrote `under+` in Z80.
  \
  \ 2015-12-21: Add `polarity`.
  \
  \ 2015-12-22: Add `%` and `u%`.
  \
  \ 2015-12-29: Add second version of `sqrt` (from Computer One
  \ Forth).
  \
  \ 2016-03-20: Add `+under`, a variant of `under+`.
  \
  \ 2016-04-05: Add `cell/`.
  \
  \ 2016-04-07: Add `bits`, generic version of `pixels`.
  \
  \ 2016-04-27: Add `sgn`, `<=>`, `either`, `neither`.
  \
  \ 2016-04-28: Fix `<=>`. Add `0max`.
  \
  \ 2016-05-01: Add `clshift`.
  \
  \ 2016-05-02: Compact the blocks to save space. Remove `sgn`
  \ because `polarity` does the same already.
  \
  \ 2016-07-14: Fix and complete credits of `any?`.
  \
  \ 2016-07-29: Add `gcd`.
  \
  \ 2016-10-16: Document `%`, `u%` and `gcd`.
  \
  \ 2016-10-24: Fix description of `split`.
  \
  \ 2016-10-24: Fix requiring `gcd`, `%` and `u%`.
  \
  \ 2016-10-24: Add `u>ud`.
  \
  \ 2016-11-14: Fix requiring `<=>`. Remove `sm/rem`, which is
  \ in the kernel. Move `8*` here from the assemblers.
  \
  \ 2016-11-17: Use `?(` instead of `[if]`. Compact the module,
  \ saving one block.
  \
  \ 2016-12-06: Simplify documentation of `between` and
  \ `within`.
  \
  \ 2016-12-20: Rename `jppushhl` to `jppushhl,` and `jpnext`
  \ to `jpnext,`, after the change in the kernel.
  \
  \ 2016-12-21: Add `odd?`. Rewrite `8*` in Z80.
  \
  \ 2016-12-22: Add `ever?`.

( under+ +under )

need ?(

[unneeded] under+ ?(

code under+  ( n1|u1 x n2|u2 -- n3|u3 x )
  D9 c, D1 c, C1 c, E1 c, 19 c, E5 c, C5 c, D9 c,
  \ exx
  \ pop de
  \ pop bc
  \ pop hl
  \ add hl,de
  \ push hl
  \ push bc
  \ exx
  jpnext,  end-code  ?)

  \ doc{
  \
  \ under+  ( n1|u1 x n2|u2 -- n3|u3 x )
  \
  \ Add _n2|u2_ to _n1|u2_, giving the sum _n3|u3_.
  \
  \ See `+under`.
  \
  \ Origin: Comus.
  \
  \ The word is written in Z80. This is an example
  \ implementation in Forth:
  \
  \ ----
  \ : under+  ( n1|u1 x n2|u2 -- n3|u3 x )
  \   rot + swap  ;
  \ ----
  \
  \ }doc

[unneeded] +under ?(

code +under  ( n1|u1 n2|u2 x -- n3|u3 x )
  D9 c, C1 c, D1 c, E1 c, 19 c, E5 c, C5 c, D9 c,
  \ exx
  \ pop bc
  \ pop de
  \ pop hl
  \ add hl,de
  \ push hl
  \ push bc
  \ exx
  jpnext,  end-code  ?)

  \ doc{
  \
  \ +under  ( n1|u1 n2|u2 x -- n3|u3 x )
  \
  \ Add _n2|u2_ to _n1|u2_, giving the sum _n3|u3_.
  \
  \ See `under+`
  \
  \ Origin: Comus.
  \
  \ The word is written in Z80. This is an example
  \ implementation in Forth:
  \
  \ ----
  \ : +under  ( n1|u1 n2|u2 x -- n3|u3 x )
  \   >r + r>  ;
  \ ----
  \
  \ }doc

  \ XXX TODO -- variant after PFE's `(under+)`:
  \ : +under  ( n1 n2 -- n1+n2 n2 )  tuck + swap  ;

( % u% u>ud within between gcd )

[unneeded] %
?\ : %  ( n1 n2 -- n3 )  100 swap */  ;  exit

  \ doc{
  \
  \ % ( n1 n2 -- n3 )
  \
  \ _n1_ is percentage _n3_ of _n2_
  \
  \ }doc

[unneeded] u%
?\ : u%  ( u1 u2 -- u3 )  >r 100 um* r> um/mod nip  ;  exit

  \ doc{
  \
  \ u% ( u1 u2 -- u3 )
  \
  \ _u1_ is percentage _u3_ of _u2_
  \
  \ }doc

[unneeded] u>ud
?\ need alias  ' 0 alias u>ud ( u -- ud )  exit

  \ doc{
  \
  \ u>ud  ( u -- ud )
  \
  \ Extend a single unsigned number _u_ to form a double
  \ unsigned number _ud_. This word is just an alias of `0`.
  \
  \ See: `s>d`.
  \
  \ }doc

need ?(

[unneeded] within ?(
: within  ( n1|u1 n2|u2 n3|u3 -- f )  over - >r - r> u<  ; ?)

  \ Credit:
  \
  \ Code from DZX-Forth.

  \ doc{
  \
  \ within  ( n1|u1 n2|u2 n3|u3 -- f )
  \
  \ Perform a comparison of a test value n1|u1 with a lower
  \ limit _n2|u2_ and an upper limit _n3|u3_, returning _true_
  \ if either (n2|u2 < n3|u3 and (n2|u2 <= n1|u1 and n1|u1 <
  \ n3|u3)) or (n2|u2 > n3|u3 and (n2|u2 <= n1|u1 or n1|u1 <
  \ n3|u3)) is true, returning _false_ otherwise.
  \
  \ Origin: Forth-94 (CORE EXT), Forth-2012 (CORE EXT).
  \
  \ }doc

[unneeded] between ?(
: between  ( n1|u1 n2|u2 n3|u3 -- f )
  over - -rot - u< 0=  ; ?)

  \ Credit:
  \
  \ http://dxforth.netbay.com.au/between.html

  \ doc{
  \
  \ between  ( n1|u1 n2|u2 n3|u3 -- f )
  \
  \ Perform a comparison of a test value _n1|u1_ with a lower
  \ limit _n2|u2_ and an upper limit _n3|u3_, returning _true_
  \ if either (n2|u2 <= n3|u3 and (n2|u2 <= n1|u1 and n1|u1 <=
  \ n3|u3)) or (n2|u2 > n3|u3 and (n2|u2 < n1|u1 or n1|u1 <
  \ n3|u3)) is true, returning _false_ otherwise.
  \
  \ }doc

[unneeded] gcd  ?exit

: gcd  ( n1 n2 -- n3 )  begin  ?dup  while  tuck mod  repeat  ;

  \ Credit:
  \
  \ Code by Will Baden (1986-04-10), from:
  \ <http://atariwiki.strotmann.de/wiki/Wiki.jsp?page=Local%20Variables>

  \ doc{
  \
  \ gcd  ( n1 n2 -- n3 )
  \
  \ _n3_ is the greatest common divisor of _n1_ and _n2_.
  \
  \ }doc

( odd? even? 8* polarity <=> )

[unneeded] odd?  ?\ : odd?   ( n -- f )  1 and     ;  exit
[unneeded] even? ?\ : even?  ( n -- f )  1 and 0=  ;  exit
  \ XXX TODO -- write in Z80

need ?(

[unneeded] 8* ?(

code 8*  ( n1 -- n2 )
  e1 c, 29 c, 29 c, 29 c, jppushhl, end-code  exit ?)
  \ pop hl
  \ add hl,hl
  \ add hl,hl
  \ add hl,hl
  \ _jp_pushhl

  \ doc{
  \
  \ 8*  ( x1 -- x2 )
  \
  \ _x2_ is the result of shifting _x1_ three bits toward the
  \ most-significant bit, filling the vacated least-significant
  \ bit with zero.
  \
  \ This is the same as `3 lshift` or `2* 2* 2*`, but faster.
  \
  \ See: `2*`, `lshift`.
  \
  \ }doc

[defined] polarity ?(

code polarity  ( n -- -1 | 0 | 1 )
  D1 c, 78 02 + c,  B0 03 + c,  CA c, ' false ,
    \ pop de
    \ ld a,d
    \ or e
    \ jp z,false_code
  CB c, 10 03 + c,  ED c, 62 c,
    \ rl d ; set carry if DE -ve
    \ sbc hl,hl ; HL=0 if DE +ve, or -1 if DE -ve
  78 05 + c,  F6 c, 01 c,  68 07 + c,  jppushhl,
    \ ld a,l
    \ or 1
    \ ld l,a ; HL=1 or -1
    \ jp push_hl
  end-code  ?)

  \ doc{
  \
  \ polarity  ( n -- -1|0|1 )
  \
  \ If _n_ is zero, return zero.
  \ If _n_ is negative, return negative one.
  \ If _n_ is positive, return positive one.
  \
  \ The word is written in Z80. This is an example
  \ implementation in Forth:
  \
  \ ----
  \ : polarity  ( n -- -1|0|1 )
  \   dup 0= ?exit  0< ?dup ?exit  1  ;
  \ ----
  \
  \ }doc

  \ Credit:
  \
  \ Assembler version of `polarity` adapted from Z88
  \ CamelForth.

[unneeded] <=>
?\ : <=>  ( n1 n2 -- -1|0|1 )  - polarity  ;

  \ doc{
  \
  \ <=>  ( n1 n2 -- -1|0|1 )
  \
  \ If _n1_ equals _n2_, return zero.
  \ If _n1_ is less than _n2_, return negative one.
  \ If _n1_ is greater than _n2_, return positive one.
  \
  \ }doc

( u<= u>= <= >= 0>= 0<= 0max )

[unneeded] u<=  ?\ : u<=  ( u1 u2 -- f )  u> 0=  ;  exit
[unneeded] u>=  ?\ : u>=  ( u1 u2 -- f )  u< 0=  ;  exit
[unneeded] <=   ?\ : <=   ( n1 n2 -- f )  > 0=   ;  exit
[unneeded] >=   ?\ : >=   ( n1 n2 -- f )  < 0=   ;  exit
[unneeded] 0>=  ?\ : 0>=  ( n1 n2 -- f )  0< 0=  ;  exit
[unneeded] 0<=  ?\ : 0<=  ( n1 n2 -- f )  0> 0=  ;  exit

[unneeded] 0max ?exit

code 0max  ( n -- n | 0 )
  E1 c,  CB c, 10 05 + c,  DA c, ' false ,  CB c, 18 05 + c,
    \ pop hl
    \ rl h ; negative?
    \ jp c,false_
    \ rr h
  jppushhl,  end-code
    \ jp push_hl

  \ Credit:
  \
  \ Idea from IsForth.

  \ doc{
  \
  \ 0max  ( n -- n | 0 )
  \
  \ If _n_ is negative, return 0; else return _n_.
  \ This is a faster alternative to the idiom `0 max`.
  \
  \ }doc

( lshift )

  \ Credit:
  \
  \ Code adapted from Z88 CamelForth.

  \ Data space used: 16 bytes.

need z80-asm

code lshift  ( x1 u -- x2 )

  exx
  bc pop  \ C = loop counter
  c b ld
  hl pop  \ hi 8 bits ignored!
  b inc  ahead 0 unresolved !
  begin  hl addp  0 unresolved @ >relresolve  step
  hl push
  exx
  jpnext,

  end-code

( lshift )

  \ Credit:
  \
  \ Code adapted from DZX-Forth.

  \ This version is 3 bytes smaller but slower (169% the
  \ execution time of version adapted from Z88 CamelForth).
  \
  \ Data space used: 13 bytes.

code lshift  ( x1 u -- x2 )

  D1 c,           \ pop de
  E1 c,           \ pop hl
  1C c,           \ inc e
  here            \ begin:
  1D c,           \ dec e
  CA c, pushhl ,  \ jp z,push_hl
  29 c,           \ add hl,hl
  C3 c, ,         \ jp begin

  end-code

( rshift )

need z80-asm

  \ Credit:
  \
  \ Code adapted from Z88 CamelForth.

  \ Data space used: 19 bytes.

code rshift  ( x1 u -- x2 )

  exx
  bc pop  \ C = loop counter
  c b ld
  hl pop  \ hi 8 bits ignored!

  b inc  ahead 0 unresolved !

  begin  h srl  l rr  0 unresolved @ >relresolve  step
  hl push
  exx
  jpnext,

  end-code

( rshift )

  \ Credit:
  \
  \ Code adapted from DZX-Forth.

  \ This version is 3 bytes smaller but slower (133% the
  \ execution time of the version adapted from Z88 CamelForth).

  \ Data space used: 16 bytes.

code rshift  ( x1 u -- x2 )
  D1 c,           \ pop de
  E1 c,           \ pop hl
  1C c,           \ inc e
  here            \ begin:
  1D c,           \ dec e
  CA c, pushhl ,  \ jp z,push_hl
  CB c, 3C c,     \ srl h
  CB c, 1D c,     \ rr l
  C3 c, ,         \ jp begin
  end-code

( clshift crshift )

need ?(

[unneeded] clshift ?(

code clshift  ( b1 u -- b2 )

  D1 c,  E1 c,  78 05 + c,  1C c,
    \ pop de
    \ pop hl
    \ ld a,l
    \ inc e
  here
    \ begin:
  1D c,  CA c, pusha ,  80 07 + c,  C3 c, ,  end-code  ?)
    \ dec e
    \ jp z,push_a
    \ add a,a
    \ jp begin

[unneeded] crshift ?(

  \ XXX UNDER DEVELOPMENT -- 2016-05-01

code crshift  ( b1 u -- b2 )

  D1 c,  E1 c,  78 05 + c,  1C c,
    \ pop de
    \ pop hl
    \ ld a,l
    \ inc e
  here
    \ begin:
  1D c,  CA c, pusha ,
    \ dec e
    \ jp z,push_a
  \ 80 07 + c,
    \ rra
  C3 c, ,  end-code  ?)
    \ jp begin

( bits )

  \ Credit:
  \
  \ Based on a pixels counter written by Juan Antonio Paz,
  \ published on Microhobby, issue 170 (1988-05), page 21:
  \ http://microhobby.org/numero170.htm
  \ http://microhobby.speccy.cz/mhf/170/MH170_21.jpg

  \ Data space used: 29 bytes.

need z80-asm

code bits  ( ca len -- u )

  0 hl ldp#  \ init bit count
  exx  \ save IP and count
  de pop  hl pop  \ memory zone
  begin
    d a ld  e or  nz if
      08 b ld#  \ bits per byte
      begin  m rrc  cy if  exx hl incp exx  then  step
      hl incp  de decp  \ next byte
  2swap again then
    \ Note: `2swap` is needed because `begin again` and `if
    \ then` are not nested.

  exx jppushhl, end-code

  \ doc{
  \
  \ bits  ( ca len -- u )
  \
  \ Count the number of bits set in memory zone _ca len_.
  \
  \ }doc

( 2/ cell/ )

  \ Credit:
  \
  \ Code from Spectrum Forth-83.
  \ Documentation partly based on lina.

need ?(

[unneeded] 2/ ?(

code 2/  ( x1 -- x2 )
  E1 c,           \ pop hl
  CB c, 2C c,     \ sra h
  CB c, 1D c,     \ rr l
  jppushhl,       \ jp pushhl
  end-code  ?)

  \ doc{
  \
  \ 2/  ( x1 -- x2 )
  \
  \ _x2_ is the result of shifting _x1_ one bit toward the
  \ least-significant bit, leaving the most-significant bit
  \ unchanged.
  \
  \ This is the same as `s>d 2 fm/mod swap drop`. It is not the
  \ same as `2 /`, nor is it the same as `1 rshift`.
  \
  \ Origin: Forth-83 (Required Word Set), Forth-94 (CORE),
  \ Forth-2012 (CORE).
  \
  \ }doc

[unneeded] cell/  ?\ need alias  need 2/  ' 2/ alias cell/

  \ Credit:
  \
  \ Idea from IsForth.

( sqrt )

  \ Credit:
  \
  \ Original code by Wil Baden, published on Forth Dimensions
  \ (volume 18, number 5, page 27, 1997-01).

  \ XXX TODO -- benchmark

need d2*  need 2/

[defined] cell-bits ?\ 16 constant cell-bits

: (sqrt)  ( radicand -- remainder root )
  0 0                           ( radicand remainder root )
  [ cell-bits 2/ ] literal 0 do
    >r d2* d2* r>               \ shift remainder left 2 bits
    2*                          \ shift root left 1 bit
    2dup 2* u> if               \ check for next bit of root
      >r r@ 2* - 1- r>          \ reduce remainder
      1+                        \ add a bit to root
    then
  loop  cr .s rot drop  ;

: sqrt  ( radicand -- root )  (sqrt) nip  ;

( sqrt )

  \ Integer square root by Newton's method

  \ Credit:
  \
  \ Adapted from Sinclair QL's Computer One Forth.

  \ XXX TODO -- benchmark

need 2/

: sqrt  ( n1 -- n2 )
  dup 0< -24 ?throw  \ invalid numeric argument
  dup
  if  dup 2/  20 0
      do      2dup / + 2/
      loop    swap drop
  then  ;

( /-rem /- -rem */-rem */- )

  \ Symmetric-division operators

  \ Credit:
  \
  \ Forth-94 documentation.

need sm/rem

: /-rem  ( n1 n2 -- n3 n4 )  >r  s>d  r> sm/rem  ;

: /-  (  n1 n2 -- n3 )  /-rem nip  ;

: -rem  ( n1 n2 -- n3 )  /-rem drop  ;

: */-rem  (  n1 n2 n3 -- n4 n5 )  >r  m*  r> sm/rem  ;

: */-  ( n1 n2 n3 -- n4 )  */-rem nip  ;

( fm/mod )

  \ Credit:
  \
  \ Code from Z88 CamelForth.

: fm/mod  ( d1 n1 -- n2 n3 )
  \ floored signed division
  dup >r                \ save divisor
  sm/rem
  over 0<> over 0< and  \ quotient<0 and remainder<>0?
  if
    swap r> +           \ add divisor to remainder
    swap 1-             \ decrement quotient
  else r> drop then  ;

  \ doc{
  \
  \ fm/mod  ( d1 n1 -- n2 n3 )
  \
  \ Floored division:
  \
  \ ----
  \   d1 = n3*n1+n2
  \   n1>n2>=0 or 0>=n2>n1
  \ ----
  \
  \ Divide _d1_ by _n1_, giving the floored quotient _n3_ and
  \ the remainder _n2_. Input and output stack arguments are
  \ signed.
  \
  \ Origin: Forth-94 (CORE), Forth-2012 (CORE).

  \ [caption="Floored Division Example"]
  \
  \ |===
  \ | Dividend  | Divisor | Remainder | Quotient

  \ >|       10  >|      7  >|        3  >|        1
  \ >|      -10  >|      7  >|        4  >|       -2
  \ >|       10  >|     -7  >|       -4  >|       -2
  \ >|      -10  >|     -7  >|       -3  >|        1
  \ |===
  \
  \ }doc

( /_mod /_ _mod */_mod */_ )

  \ Floored-division operators

  \ Credit:
  \
  \ Forth-94 documentation.

need fm/mod

: /_mod  ( n1 n2 -- n3 n4 )  >r s>d r> fm/mod  ;

: /_  ( n1 n2 -- n3 )  /_mod nip  ;

: _mod  ( n1 n2 -- n3 )  /_mod drop  ;

: */_mod  ( n1 n2 n3 -- n4 n5 )  >r m* r> fm/mod  ;

: */_  ( n1 n2 n3 -- n4 )   */_mod nip  ;

( any? either neither )

need ?(

[unneeded] any? ?(  need roll  variable (any?)

: any?  ( x0 x1..xn n -- f )
  dup 1+ roll (any?) !
  0 swap 0 do  swap (any?) @ = or  loop  ;  ?)

  \ doc{
  \
  \ any?  ( x0 x1..xn n -- f )
  \
  \ Is any _x1..xn_ equal to _x0_?
  \
  \ Origin: John A. Peters' tools for CP/M F83 2.1.1, 1984.
  \
  \ }doc

  \ Credit:
  \
  \ Originally written by John A. Peters in 1984 as part of a
  \ tool set for the CP/M implementation of Laxen&Perry's F83
  \ 2.1.1.

  \ Credit:
  \
  \ Code from IsForth (version 1.23b).

[unneeded] either ?(

: either  ( n1|u1 n2|u2 n3|u3 -- f )
  -rot over = -rot = or  ;  ?)

  \ doc{
  \
  \ either  ( n1|u1 n2|u2 n3|u3 -- f )
  \
  \ Return _true_ if _n1|u1_ equals either _n2|u2_ or _n3|u3_;
  \ else return _false_.
  \
  \ }doc

[unneeded] neither ?(

: neither  ( n1|u1 n2|u2 n3|u3 -- f )
  -rot over <> -rot <> and  ;  ?)

  \ doc{
  \
  \ neither  ( n1|u1 n2|u2 n3|u3 -- f )
  \
  \ Return _true_ if _n1|u1_ is not equal to either _n2|u2_ or
  \ _n3|u3_; else return _false_.
  \
  \ }doc

( split join )

  \ XXX OLD -- 2016-10-27: Moved to the kernel, needed by the
  \ extra memory system.

need ?(

[unneeded] split ?(

code split  ( x -- b1 b2 )
  E1 c,
    \ pop hl
  16 c, 00 c,  58 05 + c,  68 04 + c,  26 c, 00 c,
    \ ld d,0
    \ ld e,l
    \ ld l,h
    \ ld h,0
  C3 c, pushhlde ,  end-code  ?)
    \ jp push_hlde

  \ Credit:
  \
  \ Idea from IsForth.

  \ doc{
  \
  \ split  ( x -- b1 b2 )
  \
  \ Get _b1_ and _b2_ from the 2 bytes which compose _x_: _b1_
  \ is the low-order byte and _b2_ is the high-order byte.
  \
  \ See `join`.
  \
  \ Origin: IsForth.
  \
  \ }doc

[unneeded] join ?(

code join  ( b1 b2 -- x )
  D1 c,  60 03 + c,  D1 c,  68 03 + c,
    \ pop de
    \ ld h,e
    \ pop de
    \ ld l,e
  jppushhl,  end-code  ?)
    \ jp push_hl

  \ doc{
  \
  \ join  ( b1 b2 -- x )
  \
  \ _b1_ is the low-order byte of _x_, and _b2_ is the
  \ high-order byte of _x_.
  \
  \ See `split`.
  \
  \ Origin: IsForth.
  \
  \ }doc

  \ vim: filetype=soloforth
