  \ data.const.fsb
  \
  \ This file is part of Solo Forth
  \ http://programandala.net/en.program.solo_forth.html

  \ Last modified: 201611251256

  \ -----------------------------------------------------------
  \ Description

  \ Definers of so called "fast constants", which work like
  \ ordinary constants, except their value is compiled as a
  \ literal. A literal is placed on the stack faster than a
  \ constant.
  
  \ -----------------------------------------------------------
  \ Author

  \ Marcos Cruz (programandala.net), 2015, 2016.

  \ -----------------------------------------------------------
  \ Credit

  \ Words inspired by IsForth's `const`.

  \ -----------------------------------------------------------
  \ License

  \ You may do whatever you want with this work, so long as you
  \ retain every copyright, credit and authorship notice, and
  \ this license.  There is no warranty.

  \ -----------------------------------------------------------
  \ History

  \ 2016-11-25: Extract code from <data.misc.fsb>. Improve
  \ documentation.

( const cconst 2const )

need ?(

[unneeded] const ?(

: const  ( x "name" -- )
  create immediate ,
  does>  @ executing? ?exit  postpone literal  ; exit ?)

  \ doc{
  \
  \ const  ( x "name" -- )
  \
  \ Create a fast constant _name_, with value _x_.
  \
  \ A fast constant works like an ordinary constant, except its
  \ value is compiled as a literal.
  \
  \ See: `constant`, `cconst`, `2const`.
  \
  \ Origin: IsForth.
  \
  \ }doc

[unneeded] cconst ?(

: cconst  ( c "name" -- )
  create immediate c,
  does>  c@ executing? ?exit  postpone cliteral  ; exit ?)

  \ doc{
  \
  \ cconst  ( c "name" -- )
  \
  \ Create a character fast constant _name_, with value _c_.
  \
  \ A fast constant works like an ordinary constant, except its
  \ value is compiled as a literal.
  \
  \ See: `cconstant`, `const`, `2const`.
  \
  \ Origin: IsForth's `const`.
  \
  \ }doc

[unneeded] 2const ?(

: 2const  ( xd "name" -- )
  create immediate 2,
  does>  2@ executing? ?exit  postpone 2literal  ; exit ?)

  \ doc{
  \
  \ 2const  ( xd "name" -- )
  \
  \ Create a double fast constant _name_, with value _xd_.
  \
  \ A fast constant works like an ordinary constant, except its
  \ value is compiled as a literal.
  \
  \ See: `2constant`, `const`, `cconst`.
  \
  \ Origin: IsForth's `const`.
  \
  \ }doc

  \ vim: filetype=soloforth
