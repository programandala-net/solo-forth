  \ sound.48.fsb
  \
  \ This file is part of Solo Forth
  \ http://programandala.net/en.program.solo_forth.html

  \ Last modified: 201612300209

  \ -----------------------------------------------------------
  \ Description

  \ Words related to 48k sound.

  \ -----------------------------------------------------------
  \ Author

  \ Marcos Cruz (programandala.net), 2015, 2016.

  \ -----------------------------------------------------------
  \ License

  \ You may do whatever you want with this work, so long as you
  \ retain every copyright, credit and authorship notice, and
  \ this license.  There is no warranty.

  \ -----------------------------------------------------------
  \ History

  \ 2015: Main development.
  \
  \ 2016-04-14: Documented `bleep` and `beep>bleep`.
  \
  \ 2016-12-20: Rename `jpnext` to `jpnext,` after the change
  \ in the kernel.
  \
  \ 2016-12-30: Compact the code, saving two blocks.

( bleep beep>bleep )

need ?(

[unneeded] bleep ?(

code bleep  ( duration pitch -- )
  E1 c,  D1 c,  C5 c,  CD c, 03B5 ,  C1 c,  DD c, 21 c, next ,
    \ pop hl
    \ pop de
    \ push bc ; save Forth IP
    \ call rom_beeper
    \ pop bc ; restore Forth IP
    \ ld ix,next ; restore ix
  jpnext, end-code  exit ?)

  \ Credit:
  \
  \ Code modified from Abersoft Forth.

  \ doc{
  \
  \ bleep  ( duration pitch -- )
  \
  \ Produce a tone in the internal beeper.
  \
  \ }doc

[unneeded] beep>bleep ?(

: beep>bleep  ( frequency duration1 -- duration2 pitch )
  over 1000 */ swap  4375 100 rot */ 30 -  ; ?)

  \ Credit:
  \
  \ Code from v.Forth.

  \ XXX TODO test

  \ doc{
  \
  \ beep>bleep  ( frequency duration1 -- duration2 pitch )
  \
  \ Convert the parameters of Sinclair BASIC `beep` to Solo
  \ Forth `bleep`. See `bleep`.
  \
  \ }doc

( laser-gun ambulance )

[unneeded] laser-gun ?(  need z80-asm

code laser-gun  ( -- )  bc push  5 b ld#  0500 hl ldp#
  begin   0001 de ldp#
          hl push  03B5 call  hl pop  \ ROM beeper
          0010 de ldp#  de subp  jrnz
  bc pop  next ix ldp#  jpnext,  end-code  exit ?)

  \ Laser gun sound for ZX Spectrum 48K.

  \ Credit:
  \
  \ Author of the original code: Álvaro Corredor Lanas.
  \ Published on Microhobby, issue 126 (1987), page 7:
  \ http://microhobby.org/numero126.htm
  \ http://microhobby.speccy.cz/mhf/126/MH126_07.jpg

[unneeded] ambulance ?(  need z80-asm

code ambulance  ( n -- )

  \ n = times

  de pop  bc push  e b ld

  begin   bc push  0320 hl ldp#  000A de ldp#
          <mark   hl push
                  03B5 call  \ ROM beeper
                  hl pop  hl decp  hl tstp  jrnz
          bc pop  step

  bc pop  next ix ldp#  jpnext, end-code ?)

  \ Ambulance sound for ZX Spectrum 48K.

  \ Credit:
  \
  \ Author of the original code: Líder Software.
  \ Published on Microhobby, issue 142 (1987-09), page 7:
  \ http://microhobby.org/numero142.htm
  \ http://microhobby.speccy.cz/mhf/142/MH142_07.jpg

( white-noise )

  \ White noise for ZX Spectrum 48K.

  \ Credit:
  \
  \ Author of the original code: Ricardo Serral Wigge.
  \ Published on Microhobby, issue 125 (1987), page 26:
  \ http://microhobby.org/numero125.htm
  \ http://microhobby.speccy.cz/mhf/125/MH125_26.jpg

  \ The original code was called "explosion" and had a fixed
  \ duration of 768 sample bytes, thus equivalent to `768
  \ white-noise`.

need z80-asm

code white-noise  ( u -- )

  \ u = duration in number of sample bytes

  de pop
  bc push  \ save the Forth IP
  de bc ldp  0000 hl ldp#  \ bc=duration, hl=start of ROM

  5C48 fta  a sra  a sra  a sra  07 and#  a d ld
    \ d = border color (in bits 0-2)

  begin   m e ld  hl incp  bc decp  bc push
          08 b ld#  \ bit counter
          begin   e a ld  10 and#  e rl  d or  FE out  \ beep
                  step
          bc pop  bc tstp
          jrnz

  bc pop  jpnext, \ restore the Forth IP and go next

  end-code

  \ vim: filetype=soloforth
