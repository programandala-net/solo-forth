  \ time.fsb
  \
  \ This file is part of Solo Forth
  \ http://programandala.net/en.program.solo_forth.html

  \ Last modified: 201612201839

  \ -----------------------------------------------------------
  \ Description

  \ Words related to time.

  \ -----------------------------------------------------------
  \ Author

  \ Marcos Cruz (programandala.net), 2015, 2016.

  \ -----------------------------------------------------------
  \ License

  \ You may do whatever you want with this work, so long as you
  \ retain every copyright, credit and authorship notice, and
  \ this license.  There is no warranty.

  \ -----------------------------------------------------------
  \ Latest changes

  \ 2016-05-07: Make block titles compatible with `indexer`.
  \
  \ 2016-05-17: Need `>body`, which has been moved to the
  \ library.
  \
  \ 2016-11-17: Improve documentation of `frames@`, `frames!`,
  \ `reset-frames`.
  \
  \ 2016-12-16: Add `seconds`.
  \
  \ 2016-12-20: Rewrite `ms`, after the v.Forth version.
  \ Prepare several variants of `pause`.

( seconds ms )

[unneeded] seconds
?\ need ms  : seconds  ( n -- )  1000 * ms  ;  exit

  \ doc{
  \
  \ seconds  ( n -- )
  \
  \ Wait _n_ seconds.
  \
  \ }doc

[unneeded] ms ?exit

need z80-asm

code ms  ( u -- )
  de pop  de tstp nz if
    begin  #171 a ld#  begin  nop a dec  z until
           de decp  de tstp
    z until
  then  jpnext  end-code

  \ Credit:
  \ Code adapted from v.Forth:
  \
  \ code ms
  \  pop de
  \  begin,
  \   ld a,171
  \   begin,
  \    nop
  \    dec a
  \   -until,
  \   dec de
  \   ld a,d
  \  ora e
  \  -until,
  \  jp (ix)

  \ XXX TODO -- support for multitasking

  \ doc{
  \
  \ ms  ( u -- )
  \
  \ Wait at least _u_ ms (miliseconds).
  \
  \ Origin: Forth-94 (FACILITY EXT), Forth-202 (FACILITY
  \ EXT).
  \
  \ }doc

  \ --------------------------------------------
  \ Old first version

  \ XXX OLD -- This code depends on the system interrupts

  \ need os-frames
  \ : ms  ( u -- )
  \   20 / os-frames @ +
  \   begin  dup os-frames @ u<  until drop  ;
  \   \ Wait at least _u_ ms (miliseconds), with 20 ms precision.

  \ --------------------------------------------
  \ Version from Spectrum Forth-83

  \ : ms  ( u -- )  20 /mod pause 0 ?do 10 0
  \   do loop loop  ;

  \ --------------------------------------------
  \ Version from DZX-Forth (for 8080)

  \ code ms  ( u -- )  hex
  \   d1 c,         \ pop de
  \   here          \ ms1:
  \   78 03 + c,  B0 02 + c,
  \                 \ ld a,e
  \                 \ or d
  \   ca c, next ,  \ jp z,next
  \   21 c, 0004 ,  \ ld hl,4 ; mhz
  \   29 c,  29 c,  29 c,
  \                 \ add hl,hl
  \                 \ add hl,hl
  \                 \ add hl,hl
  \   here          \ ms2:
  \   e3 c,  e3 c,  e3 c,  e3 c,
  \                 \ ex (sp),hl        ; 19t
  \                 \ ex (sp),hl        ; 19t
  \                 \ ex (sp),hl        ; 19t
  \                 \ ex (sp),hl        ; 19t
  \   e5 c,  e1 c,  2B c,
  \                 \ push hl           ; 11t
  \                 \ pop hl            ; 10t
  \                 \ dec hl            ;  6t
  \   3E c, 00 c,  78 05 + c,  B0 04 + c,
  \                 \ ld a,0            ;  7t
  \                 \ ld a,l            ;  4t
  \                 \ or h              ;  4t
  \   c2 c, ,       \ jp nz,ms2         ; 10t
  \   1B c,         \ dec de
  \   c3 c, ,       \ jp ms1
  \ end-code

( ms88 )

  \ --------------------------------------------
  \ Version of `ms` adapted from Z88 CamelForth

  \ XXX TODO
  \ XXX UNDER DEVELOPMENT

  \ Original:
  \
    \ defw PAUSE  ; allow other tasks to run
  \ .stMS:
    \ defw stMS+2
    \ ld l,10    ; only run other tasks every 10ms
  \ .reMS:
    \ ld a,b
    \ or c
    \ jr z,endMS
    \ push bc
    \ ld bc,133
  \ .reMS2:
    \ dec bc
    \ ld a,b
    \ or c
    \ jr nz,reMS2
    \ pop bc
    \ dec bc
    \ dec l
    \ jr nz,reMS
    \ ld de,stMS
    \ jp PAUSE  ; allow other tasks to run
  \ .endMS  pop bc          ; get new TOS
    \ jp EXIT

need z80-asm
code ms88  ( u -- )
  de pop
    \ ld l,10    ; only run other tasks every 10ms
  begin \ .reMS:

  d a ld
  e or nz if

    de push \ push bc
    #133 de ldp#
    begin  \ .reMS2:
      dec decp  d a ld  e or
    z until \ jr nz,reMS2
    de pop \ pop bc
    de decp  l dec
 z until \ jr nz,reMS
    \ ld de,stMS
    \ jp PAUSE  ; allow other tasks to run
  \ .endMS
  then  jpnext  end-code

( frames@ frames! reset-frames )

need os-frames

: frames@  ( -- d )
  os-frames @ [ os-frames cell+ ] literal c@  ;

  \ doc{
  \
  \ frames@  ( -- d )
  \
  \ Fetch the system frames counter, which is incremented every
  \ 20 ms by the OS.
  \
  \ See: `frames!`, `reset-frames`.
  \
  \ }doc

: frames!  ( d -- )
  [ os-frames cell+ ] literal c! os-frames !  ;

  \ doc{
  \
  \ frames!  ( d -- )
  \
  \ Store _d_ at the system frames counter, which is
  \ incremented every 20 ms by the OS.
  \
  \ See: `frames@`, `reset-frames`.
  \
  \ }doc

: reset-frames  ( -- )  0. frames!  ;

  \ doc{
  \
  \ reset-frames  ( -- )
  \
  \ Reset the system frames counter, which is incremented every
  \ 20 ms by the OS, seeting it to zero.
  \
  \ See: `frames@`, `frames!`.
  \
  \ }doc

( pause )

need os-frames
: pause  ( u -- )
  os-frames @ +
  begin  dup os-frames @ u< key? or  until drop  ;
  \ Wait at least _u_ system frames
  \ or until a key is pressed.

need os-frames
: do-pause  ( u -- )
  os-frames @ +
  begin  dup os-frames @ u<  until drop  ;
  \ Wait at least _u_ system frames.

( user-pause )

need z80-asm,  need call-xt,  need execute-hl,  need user

user (user-pause)  ' noop (user-pause) !

code user-pause ( u -- )
  d pop, b push,
  rbegin
    d push,
    ' (user-pause) call-xt, ' @ call-xt, hl pop, execute-hl,
    d pop, halt, d decp, d tstp,
  z? runtil
  b pop,  jpnext  end-code

  \ Credit:
  \
  \ Code adapted from Spectrum Forth-83.

  \ doc{
  \
  \ user-pause  ( u -- )
  \
  \ Stop execution during _u_ frames of the TV (there are 50
  \ frames per second in in Europe and 60 frames per second in
  \ USA).
  \
  \ During the pause, the execution token hold in the
  \ `(user-pause)` user variable is executed once for every
  \ _u_.
  \
  \ See: `defer-pause`, `key-pause`, `do-pause`.
  \
  \ }doc

( do-pause )

  \ Credit:
  \
  \ Code adapted from Spectrum Forth-83.

need ?(

[unneeded] do-pause ?(  need z80-asm,

code do-pause ( u -- )
  d pop, b push,
  rbegin  halt, d decp, d tstp,  z? runtil
  b pop, jpnext  end-code  exit ?)

  \ doc{
  \
  \ do-pause  ( u -- )
  \
  \ Stop execution during _u_ frames of the TV (there are 50
  \ frames per second in in Europe and 60 frames per second in
  \ USA).
  \
  \ See: `defer-pause`, `key-pause`, `user-pause`.
  \
  \ }doc

( key-pause )

need ?(

[unneeded] key-pause ?(  need z80-asm,

code key-pause ( u -- )
  d pop, b push,
  cr .( before rbegin:) cr space here u. .s
  1 iy 5 resx, \ forget last key (system variable FLAGS)
  rbegin

-->

( key-pause )

  cr .( after rbegin:) cr space here u. .s
    halt,
    1 iy 5 bitx, \ new key available? (system variable FLAGS)
  cr .( before rif:) cr space here u. .s
    z? rif
  cr .( after rif:) cr space here u. .s
            d decp, d tstp,
            2swap  \ force resolve `rbegin` before `rif`
  cr .( before runtil:) cr space here u. .s
            z? runtil
  cr .( after runtil/before rthen:) cr space here u. .s
            rthen
  cr .( after rthen:) cr space here u. .s
  b pop, jpnext  end-code  exit ?)

  \ doc{
  \
  \ key-pause  ( u -- )
  \
  \ Stop execution during _u_ frames of the TV (there are 50
  \ frames per second in in Europe and 60 frames per second in
  \ USA), or until a key is pressed.
  \
  \ See: `defer-pause`, `user-pause`, `do-pause`.
  \
  \ }doc

( defer-pause )

  \ Credit:
  \
  \ Code adapted from Spectrum Forth-83.

  \ XXX TODO -- rename

need z80-asm  need call-xt

defer (defer-pause)  ' noop ' (defer-pause) defer!

code defer-pause ( u -- )
  de pop  bc push
  begin
    de push  ' (defer-pause) call-xt
    de pop  halt  de decp  de tstp
  z until
  bc pop  jpnext  end-code

  \ doc{
  \
  \ defer-pause  ( u -- )
  \
  \ Stop execution during _u_ frames of the TV (there are 50
  \ frames per second in in Europe and 60 frames per second in
  \ USA).
  \
  \ During the pause, the deferred word `(defer-pause)` is
  \ executed once for every _u_.
  \
  \ See: `user-pause`, `key-pause`, `do-pause`.
  \
  \ }doc

( leapy-year? )

  \ 2015-11-15

  \ Credit:
  \
  \ Code written by Wil Baden, published on Forth Dimensions
  \ (volume 8, number 5, page 31, 1987-01).

: leapy-year?  ( n -- f )
  dup 400 mod 0= if  drop true   exit  then
  dup 100 mod 0= if  drop false  exit  then
        4 mod 0= if       false  exit  then
  false  ;

exit

  \ Alternative:

need baden-case
  \ XXX TODO -- update to the new syntax

: leapy-year?  ( n -- f )
  case 400 mod 0= of  true   endof
  case 100 mod 0= of  false  endof
  case   4 mod 0= of  true   endof
  othercase false  ;

( set-date get-date )

create (date)  1 c,  1 c,  2016 ,
  \ day, month, year

: get-date  ( -- day month year )
  (date) c@
  [ (date) 1+ ] literal c@
  [ (date) 2+ ] literal @  ;

  \ doc{
  \
  \ get-date  ( -- day month year )
  \
  \ Get the current date. The default date is 2016-01-01. It
  \ can be changed with `set-date`. The date is not updated by
  \ the system.
  \
  \ }doc

: set-date  ( day month year -- )
  [ (date) 2+ ] literal !
  [ (date) 1+ ] literal c!
  (date) !  ;

  \ doc{
  \
  \ set-date  ( day month year -- )
  \
  \ Set the current date. The default date is 2016-01-01. It
  \ can be fetch with `get-date`. The date is not updated by
  \ the system.
  \
  \ }doc

( set-time get-time reset-time )

need frames@  need frames!  need m+  need alias  need ud*

: get-time  ( -- second minute hour )
  frames@ 50 um/mod nip s>d   ( sec . )
          60 um/mod s>d       ( sec min . )
          60 um/mod           ( sec min hour )  ;

  \ doc{
  \
  \ get-time  ( -- second minute hour )
  \
  \ Return the current time.
  \
  \ The system doesn't have an actual clock. The system frames
  \ counter is used instead. It is increased by the interrupts
  \ routine every 20th ms. The counter is a 24-bit value, so
  \ its maximum is $FFF ticks of 20 ms (5592 minutes, 93
  \ hours), then it starts again from zero.
  \
  \ }doc

: set-time  ( second minute hour -- )
  3600 um*  rot 60 * m+  rot m+  ( seconds )
  50. ud* frames!  ;

  \ doc{
  \
  \ set-time  ( second minute hour -- )
  \
  \ Set the current time. See `get-time`.
  \
  \ }doc

' reset-frames alias reset-time  ( -- )

  \ doc{
  \
  \ reset-time  ( -- )
  \
  \ Reset the current time to 00:00:00. See `get-time`.
  \
  \ }doc

( .time .system-time .date .system-date .time&date time&date )

  \ XXX TODO document

need get-time  need get-date

: .00  ( n -- )  s>d <# # # #> type  ;
: .0000  ( n -- )  s>d <# # # # # #> type  ;

: .time  ( second minute hour -- )
  .00 ':' emit .00 ':' emit .00  ;

: .system-time  ( -- )  get-time .time  ;

: .date  ( day month year -- )
  .0000 '-' emit .00 '-' emit .00  ;

: .system-date  ( -- )  get-date  .date  ;

: .time&date  ( second minute hour day month year -- )
  .date 'T' emit .time  ;

: time&date  ( -- second minute hour day month year )
  get-time get-date  ;

  \ doc{
  \
  \ time&date  ( -- second minute hour day month year )
  \
  \ Return the current time and date: second (0..59), minute
  \ (0..59), hour (0..23), day (1..31), month (1..12) and year
  \ (e.g., 2016).
  \
  \ See: `get-time`, `get-date`, `set-time`, `set-date`.
  \
  \ Origin: Forth-94 (FACILITY EXT), Forth-201 (FACILITY
  \ EXT).
  \
  \ }doc

  \ vim: filetype=soloforth
