  \ flow.MISC.fsb
  \
  \ This file is part of Solo Forth
  \ http://programandala.net/en.program.solo_forth.html

  \ Last modified: 201701190050

  \ -----------------------------------------------------------
  \ Description

  \ Miscellaneous control flow structures that can be defined
  \ in less than one block.

  \ -----------------------------------------------------------
  \ Author

  \ Marcos Cruz (programandala.net), 2015, 2016, 2017.

  \ -----------------------------------------------------------
  \ License

  \ You may do whatever you want with this work, so long as you
  \ retain every copyright, credit and authorship notice, and
  \ this license.  There is no warranty.

  \ -----------------------------------------------------------
  \ History

  \ 2016-11-26: Create this module to combine the modules that
  \ contain small control flow structures, in order to save
  \ blocks: <flow.base-execute.fsb>, <flow.call.fsb>,
  \ <flow.dont.fsb>, <flow.executions.fsb>,
  \ <flow.question-question.fsb>, <flow.question-repeat.fsb>,
  \ <flow.recurse.fsb>, <flow.retry.fsb>.
  \
  \ 2016-11-26: Document `base-execute`.  Compact `call` and
  \ document it. Document `??`. Move `?leave` from the kernel.
  \
  \ 2016-12-03: Fix needing of `retry`.
  \
  \ 2016-12-04: Add `+perform`.
  \
  \ 2016-12-08: Rename the module filename with uppercase
  \ "MISC" after the new convention.
  \
  \ 2016-12-20: Rename `jpnext` to `jpnext,` after the change
  \ in the kernel.
  \
  \ 2017-01-18: Remove `exit` at the end of conditional
  \ interpretation.

( +perform base-execute call don't executions )

[unneeded] +perform  ( a n -- )
?\ : +perform  ( a n -- )  cells + perform  ;

  \ doc{
  \
  \ +perform  ( a n -- )
  \
  \ Execute the execution token pointed by an offset of _n_
  \ cells from base address _a_, i.e., execute the contents of
  \ element _n_ of the cell table that starts at _a_.
  \
  \ If the execution token is zero, do nothing.
  \
  \ See: `perform`, `execute`.
  \
  \ }doc

[unneeded] base-execute
?\ : base-execute  ( xt n -- )  base @ >r execute r> base !  ;

  \ Credit:
  \
  \ Word from Gforth.

  \ doc{
  \
  \ base-execute  ( xt n -- )
  \
  \ Execute _xt_ with the content of `base` being _n_
  \ and restoring the original `base` afterwards.
  \
  \ }doc

need ?(  [unneeded] call ?(

code call  ( a -- )
  E1 c,  C5 c,  CD c, >mark  C1 c,  DD c, 21 c, next , jpnext,
                      >resolve E9 c,  end-code ?)

  \   pop hl
  \   push bc
  \   call call_hl
  \   pop bc
  \   ld ix,next
  \   jp next
  \ call_hl:
  \   jp (hl)

  \ doc{
  \
  \ call  ( a -- )
  \
  \ Call a machine code subroutine at _a_.
  \
  \ }doc

[unneeded] don't ?(

: don't  ( n1 n2 -- | n1 n2 )
  2dup = if  2drop unnest unnest  then  ; compile-only  exit ?)

  \ doc{
  \
  \ don't  ( n1 n2 -- | n1 n2 )
  \
  \ If _n1_ equals _n2_, remove them and exit the definition
  \ that called `don't`, else leave the _n1_ and _n2_ on the
  \ stack.
  \
  \ This word is intended to be used before `do`, as an
  \ alternative to `?do`, when the do-loop structure is
  \ factored in its own word.
  \
  \ }doc

[unneeded] executions ?(  need 2rdrop

  \ Credit:
  \
  \ Code from Galope (module times.fs).

: executions  ( xt n -- )
  2>r begin   2r@   while
        2r> 1- 2>r execute  repeat  drop 2rdrop  ; ?)

  \ doc{
  \
  \ executions  ( xt n -- )
  \
  \ Execute _xt_ _n_ times.
  \
  \ See: `times`, `dtimes`.
  \
  \ }doc

( ?repeat recurse ?? )

need ?(  [unneeded] ?repeat ?(  need cs-pick

  \ Credit:
  \
  \ Code from the documentation of Forth-2012 and Forth-94.

  \ Old history:
  \
  \ 2016-03-04: Copy the code of `?repeat` from the Forth-2012
  \ documentation.
  \ 2016-04-28: Fix the stack notation of `?repeat`.
  \ 2016-11-26: Move to <flow.misc.fsb>.

: ?repeat  ( Compilation: dest -- dest )
           ( Execution:   f -- )
  0 cs-pick   postpone until  ; immediate  exit ?)

  \ doc{
  \
  \ ?repeat  ( Compilation: dest -- dest )
  \          ( Execution:   f -- )
  \
  \ An alternative exit point for `begin until` loops.
  \
  \ Usage example:
  \
  \ ----
  \ : xx  ( -- )
  \     begin
  \       ...
  \     flag ?repeat  \ Go back to `begin` if flag is false
  \       ...
  \     flag ?repeat  \ Go back to `begin` if flag is false
  \       ...
  \     flag until    \ Go back to `begin` if flag is false
  \     ...
  \   ;
  \ ----

  \ }doc

[unneeded] recurse ?(

  \ Old history:
  \
  \ 2015-06-05: Written in the kernel.
  \ 2016-04-17: Moved to the library.
  \ 2016-04-24: Fixed with `latestxt`: now it can be used
  \ in words created with `:noname`.
  \ 2016-11-26: Move to <flow.misc.fsb>.

: recurse  ( -- )
  latestxt compile,  ; immediate compile-only  exit ?)

  \ doc{
  \
  \ recurse  ( -- )
  \
  \ Append the execution semantics of the current definition to
  \ the current definition.
  \
  \ Origin: Forth-83 (Controlled Reference Words), Forth-94
  \ (CORE), Forth-2012 (CORE).
  \
  \ }doc

[unneeded] ?? ?(

  \ Credit:
  \
  \ Original code by Neil Bawd, presented at FORML 1986.

  \ The original code was written two ways:

  \ : ??  ( Compilation: "name" -- ) ( Runtime: f -- )
  \   s" if" evaluate  bl word count evaluate
  \   s" then" evaluate
  \   ;  immediate

  \ : ??  ( Compilation: "name" -- ) ( Runtime: f -- )
  \   postpone if bl word count evaluate  postpone then
  \   ;  immediate

  \ XXX OLD -- This first version used `postpone` and
  \ `compile,` instead of `evaluate`.

  \ : ??  ( Compilation: "name" -- ) ( Runtime: f -- )
  \   postpone if
  \   parse-name find-name 0= -13 ?throw compile,
  \   postpone then
  \   ;  immediate

  \ XXX OLD -- simpler:

  \ : ??  ( Compilation: "name" -- ) ( Runtime: f -- )
  \  postpone if  ' compile,  postpone then
  \  ;  immediate

  \ XXX OLD -- even simpler:

  \ : ??  ( f -- )  0= if  r> cell+ >r  then  ; compile-only

  \ Final version, after a comment by Anton Ertl in
  \ comp.lang.forth, 2015-10-19:

: ??  ( Compilation: "name" -- ) ( Runtime: f -- )
  postpone if
  defined ( nt | 0 ) ?dup 0= -13 ?throw
  name>immediate? ( xt f ) if  execute  else  compile,  then
  postpone then
  ;  immediate compile-only

  \ XXX TODO -- 2016-11-26: don't compile `if then` for
  \ immediate _name_; compile `drop` instead?

  \ XXX TODO -- 2016-11-26: It seems more useful the old
  \ version, extended as the rest of alternative conditionals:
  \
  \ : ??   ( f -- )   0= if  r> cell+ >r  then  ; compile-only
  \ : 0??  ( f -- )      if  r> cell+ >r  then  ; compile-only
  \ : -??  ( f -- )  0>= if  r> cell+ >r  then  ; compile-only
  \ : +??  ( f -- )   0< if  r> cell+ >r  then  ; compile-only

  \ doc{
  \
  \ ??  ( Compilation: "name" -- ) ( Runtime: f -- )
  \
  \ Compilation:
  \
  \ Parse _name_ and search the current search order for it.
  \ If not found, throw exception #-13. If found and it's an
  \ immediate word, execute it, else compile it.
  \
  \ Runtime:
  \
  \ If _f_ is not zero, execute _name_, which was compiled.
  \
  \ }doc

( retry ?retry ?leave )

  \ Description:
  \
  \ Unconditional and conditional branches to the start of the
  \ current definition.

  \ Credit:

  \ Based on the article "RETRY, EXIT, and Word-Level
  \ Factoring", by Richard Astle, published on Forth Dimensions
  \ (volume 17, number 4, page 19, 1995-11).

  \ Old history:
  \
  \ 2015-11-07: First version.
  \ 2016-04-26: Documented.
  \ 2016-11-26: Move to <flow.misc.fsb>.

need ?(

[unneeded] retry ?(  need name>body

: retry  ( Compilation: -- ) ( Run-time: -- )
  latest name>body postpone again
  ; immediate compile-only  exit ?)

  \ doc{
  \
  \ retry  ( -- )
  \
  \ Do an unconditional branch to the start of the word.
  \
  \ }doc

[unneeded] ?retry ?(  need retry

: ?retry  ( Compilation: -- ) ( Run-time: f -- )
  postpone if  postpone retry  postpone then
  ; immediate compile-only  exit ?)

  \ doc{
  \
  \ : ?retry  ( Compilation: -- ) ( Run-time: f -- )
  \
  \ Do a conditional branch to the start of the word.
  \
  \ }doc

[unneeded] ?leave ?(

code ?leave  ( f -- ) ( R: loop-sys -- | loop-sys )
  E1 c, 78 04 + c, B0 05 + c, C2 c, ' leave , jpnext,
  \ pop hl
  \ ld a,h
  \ or l
  \ jp nz,leave_
  \ jp next
  end-code ?)

  \ doc{
  \
  \ ?leave  ( f -- ) ( R: loop-sys -- | loop-sys )
  \
  \ If _f_ is non-zero, discard the loop-control parameters for
  \ the current nesting level and continue execution
  \ immediately following the innermost syntactically enclosing
  \ `do`..`loop` or `do`..`+loop`.
  \
  \ See: `leave`, `unloop`, `do`, `loop`, `+loop`, `?do`.
  \
  \ }doc

  \ vim: filetype=soloforth
