  \ math.number.print.fsb
  \
  \ This file is part of Solo Forth
  \ http://programandala.net/en.program.solo_forth.html

  \ Last modified: 201702221550

  \ -----------------------------------------------------------
  \ Description

  \ Words related to number printing.

  \ -----------------------------------------------------------
  \ Author

  \ Marcos Cruz (programandala.net), 2015, 2016, 2017.

  \ -----------------------------------------------------------
  \ License

  \ You may do whatever you want with this work, so long as you
  \ retain every copyright, credit and authorship notice, and
  \ this license.  There is no warranty.

  \ -----------------------------------------------------------
  \ Latest changes

  \ 2016-05-01: Add `holds`.
  \
  \ 2016-11-24: Move `u.r` from the kernel and document it.
  \ Document also `ud.r` and `ud.`. Improve individual needing
  \ of all of them.
  \
  \ 2016-12-08: Fix `u.r`. Fix needing of `ud.`.
  \
  \ 2016-12-30: Compact the code, saving one block.
  \
  \ 2017-01-18: Remove `exit` at the end of conditional
  \ interpretation.
  \
  \ 2017-01-19: Remove remaining `exit` at the end of
  \ conditional interpretation. Rename `(d.)` to `(d.`,
  \ `(dbin.)` to `(dbin.` and `(dhex.)` to `(dhex.`.  Make all
  \ words individually accessible to `need`.
  \
  \ 2017-02-19: Replace `do`, which has been moved to the
  \ library, with `?do`.

( ud.r u.r ud. holds )

[unneeded] ud.r ?(
: ud.r ( ud n -- ) >r <# #s #> r> over - 0 max spaces type ;
?)

  \ Credit:
  \
  \ Code adapted from Spectrum Forth-83.

  \ doc{
  \
  \ ud.r ( ud n -- )
  \
  \ Print an usigned double number _ud_ right justified in a
  \ field of _n_ characters wide. If the number of characters
  \ required to print _ud_ is greater than _n_, all digits are
  \ displayed with no leading spaces in a field as wide as
  \ necessary.
  \
  \ }doc

[unneeded] u.r ?( need u>ud need ud.r
: u.r ( u n -- ) >r u>ud r> ud.r ; ?)

  \ Credit:
  \
  \ Code adapted from Spectrum Forth-83.

  \ doc{
  \
  \ u.r ( u n -- )
  \
  \ Print an unsigned number _u_ right justified in a field of
  \ _n_ characters wide. If the number of characters required
  \ to print _u_ is greater than _n_, all digits are displayed
  \ with no leading spaces in a field as wide as necessary.
  \
  \ Origin: Forth-79 (Reference Word Set)footnote:[In Forth-79,
  \ if the number of characters required to display _u_ is
  \ greater than _n_, no leading spaces are given.], Forth-83
  \ (Controlled Reference Word)footnote:[In Forth-83, if the
  \ number of characters required to display _u_ is greater
  \ than _n_, an error condition exists, which depends on the
  \ system.], Forth-94 (CORE EXT), Forth-2012 (CORE EXT).
  \
  \ }doc

[unneeded] ud. ?( need ud.r
: ud. ( ud -- ) 0 ud.r space ; ?)

  \ Credit:
  \
  \ Code adapted from Spectrum Forth-83.

  \ doc{
  \
  \ ud. ( ud -- )
  \
  \ Print an usigned double number _ud_.
  \
  \ }doc

[unneeded] holds ?(

: holds ( ca len -- )
  begin  dup  while  1- 2dup + c@ hold  repeat 2drop ; ?)

  \ Credit:
  \ Code from the documentation of Forth-2012.

  \ doc{
  \
  \ holds ( ca len -- )
  \
  \ Add string _ca len_ to the pictured numeric output string.
  \
  \ Origin: Forth-2012 (CORE EXT).
  \
  \ }doc

( base. bin. dec. hex. )

  \ Credit:
  \
  \ Code modified from eForth.

[unneeded] base.
?\ : base. ( -- ) does> c@ base @ >r base ! u. r> base ! ;

[unneeded] bin.
?\ need base.  create bin. ( n -- )  #2 c, base.

[unneeded] hex.
?\ need base.  create hex. ( n -- ) #16 c, base.

[unneeded] dec.
?\ need base.  create dec. ( n -- ) #10 c, base.
  \ XXX TMP -- `dec.` is in the kernel

( base' (d. <hex hex> (dhex. 8hex. 16hex. 32hex. )

  \ Credit:
  \
  \ Code adapted from lina.

[unneeded] base' ?\ variable base'

[unneeded] (d.
?\ : (d. ( d n -- ca len ) <# 0 ?do  #  loop  #> ;

[unneeded] <hex [unneeded] hex> and dup dup ?\ need base'
?\ : <hex ( -- ) base @ base' ! hex ; \ switch to hex
?\ : hex> ( -- ) base' @ base ! ; \ and back

[unneeded] (dhex. dup ?\ need <hex need (d.
?\ : (dhex. ( d n -- ) <hex (d. hex> type space ;

[unneeded] 32hex.
?\ need (dhex.  : 32hex.   ( d -- )     8 (dhex. ;

[unneeded] 16hex.
?\ need (dhex.  : 16hex.   ( n -- ) s>d 4 (dhex. ;

[unneeded] 8hex.
?\ need (dhex.  : 8hex.    ( b -- ) s>d 2 (dhex. ;

( binary <bin bin> (dbin. 8bin. 16bin. 32bin. )

  \ Credit:
  \
  \ Code inspired by lina.

[unneeded] binary ?\ : binary ( -- ) 2 base ! ;

[unneeded] <bin [unneeded] bin> and dup dup
?\ need base' need binary
?\ : <bin ( -- ) base @ base' ! binary ; \ switch to binary
?\ : bin> ( -- ) base' @ base ! ; \ and back

[unneeded] (dbin. dup
?\ need <bin need (d.
?\ : (dbin. ( d n -- ) <bin (d. bin> type space ;

[unneeded] 32bin.
?\ need (dbin.  : 32bin. ( d -- )     #32 (dbin. ;

[unneeded] 16bin.
?\ need (dbin.  : 16bin. ( n -- ) s>d #16 (dbin. ;

[unneeded] 8bin.
?\ need (dbin.  : 8bin.   ( b -- ) s>d  #8 (dbin. ;

  \ vim: filetype=soloforth
