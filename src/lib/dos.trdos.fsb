  \ dos.trdos.fsb
  \
  \ This file is part of Solo Forth
  \ http://programandala.net/en.program.solo_forth.html

  \ Last modified: 201702120030

  \ -----------------------------------------------------------
  \ Description

  \ TR-DOS support.

  \ -----------------------------------------------------------
  \ Author

  \ Marcos Cruz (programandala.net), 2016, 2017.

  \ -----------------------------------------------------------
  \ License

  \ You may do whatever you want with this work, so long as you
  \ retain every copyright, credit and authorship notice, and
  \ this license.  There is no warranty.

  \ -----------------------------------------------------------
  \ History

  \ 2016-08-04: Start: `dos-drive`, `get-drive`, `set-drive`.
  \
  \ 2017-02-05: Start from scratch. Move `get-drive` from the
  \ TR-DOS kernel and rewrite it in Z80. Move `cat` from the
  \ TR-DOS kernel.
  \
  \ 2017-02-10: Add DOS command constants. Add draft words to
  \ manipulate files. Rename "filedescriptor" notation to
  \ "fda". Add draft of `file-status`.
  \
  \ 2017-02-11: Fix `cat`.
  \
  \ 2017-02-12: Fix and document `file-status`.

( --dos-comands-- )

  \ TR-DOS command codes

$00 cconstant dos-init-interface
$01 cconstant dos-init-drive
$02 cconstant dos-seek-track
$03 cconstant dos-set-sector
$04 cconstant dos-define-buffer
$05 cconstant dos-read-sectors
$06 cconstant dos-write-sectors
$07 cconstant dos-cat
$08 cconstant dos-read-file-descriptor
$09 cconstant dos-write-file-descriptor
$0A cconstant dos-find-file
$0B cconstant dos-create-file
$0C cconstant dos-save-basic-program
$0E cconstant dos-read-file
$12 cconstant dos-delete-file  -->

( --dos-commands-- )

  \ TR-DOS command codes (continued)

$13 cconstant dos-copy-from-hl-to-descriptor
$14 cconstant dos-copy-from-descriptor-to-hl
$15 cconstant dos-test-track
$16 cconstant dos-select-bottom-side
$17 cconstant dos-select-top-side
$18 cconstant dos-read-system-track

: --dos-commands-- ;

( get-drive cat )

[unneeded] get-drive

?\ code get-drive  ( -- b )  3A c, 5CF6 , pusha jp,  end-code

  \ Note: $5CF6 is the TR-DOS variable "current temporary
  \ drive".

  \ doc{
  \
  \ get-drive ( -- b )
  \
  \ Return the number _b_ of the current drive (0..3).
  \
  \ This word is written in Z80. Its equivalent code in Forth
  \ is the following:

  \ ----
  \ : get-drive ( -- b )
  \   $5FC6 c@  ;
  \ ----

  \ See: `set-drive`.
  \
  \ }doc

need ?(

[unneeded] cat ?(

code cat  ( -- ior )
  3E c, 07 c, 08 c, 3E c, 02 c, dos-preserve-ip-routine call,
  \ ld a,trdos_command.cat
  \ ex af,af'
  \ ld a,2    ; stream: screen
  \ call dos.preserve_ip

  \ DD c, 21 c, next , \ ld ix,next ; restore Forth IX
  \ XXX REMARK -- No need to restore IX, the cat command does
  \ not use it.

  pushdosior jp, end-code ?)
  \ jp push_dos_ior

  \ doc{
  \
  \ cat  ( -- ior )
  \
  \ Print a catalog of the current disk.
  \
  \ See: `set-drive`.
  \
  \ }doc

( fda )

  $5CDD dup constant fda
        dup constant filename
         1+ constant filetype

: filename>fda  ( ca len -- )
  -filename  /filename min fda smove  ;

( file> )

  \ XXX UNDER DEVELOPMENT

need assembler  need --dos-comands--  need fda

code (file>)  ( -- ior )

  b push, dos-find-file a ld#, exaf, dos-routine call,
  \ A = directory entry of the file, or $FF if not found
  a inc, z? rif  1 a ld#, a and,  \ error: "no files"
  relse  a dec, \ restore file descriptor (0..127)
         dos-read-file-descriptor c ld, dos-c-routine call,
         nz? rif  a xor, 5CF9 sta, 16384 h ldp, FF a ld,
                  \ set load flag
                  \ set load address
                  \ take length from the directory
                  dos-read-file c ld, dos-c-routine call,
             rthen
  rthen b pop, pushdosior jp, end-code


: file>  ( ca len -- ior )  filename>fda (file>)  ;

( file-status )

need assembler  need --dos-comands--  need fda

code (file-status)  ( -- a ior )

  fda h ldp#, h push, b push,
    \ Push `fda` (the _a_ returned) and save Forth IP.
  dos-find-file a ld#, exaf, dos-routine call,
    \ A = directory entry (0..127), or $FF if file not found
  a inc, z? rif  1 a ld#, a and,  \ error: "no files"
  relse  a dec, \ restore directory entry (0..127)
         dos-read-file-descriptor c ld#, dos-c-routine call,
         c a ld, \ possible error code
  rthen b pop, next ix ldp#, pushdosior jp, end-code

: file-status  ( ca len -- a ior )
  filename>fda (file-status) ;

  \ doc{
  \
  \ file-status  ( -- a ior )
  \
  \ Return the status of the file identified by the character
  \ string _ca len_. If the file exists, _ior_ is zero and _a_
  \ is the address returned by `fda`, the TR-DOS File
  \ Description Area; otherwise _ior_ is the corresponding I/O
  \ result code and _a_ is useless.
  \
  \ Origin: Forth-94 (FILE-EXT), Forth-2012 (FILE-EXT).
  \
  \ See: `fda`.
  \
  \ }doc

  \ vim: filetype=soloforth
