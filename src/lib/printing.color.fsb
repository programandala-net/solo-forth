  \ printing.color.fsb
  \
  \ This file is part of Solo Forth
  \ http://programandala.net/en.program.solo_forth.html

  \ Last modified: 201612031629

  \ -----------------------------------------------------------
  \ Description

  \ Words related to color.

  \ -----------------------------------------------------------
  \ Author

  \ Marcos Cruz (programandala.net), 2015, 2016.

  \ -----------------------------------------------------------
  \ License

  \ You may do whatever you want with this work, so long as you
  \ retain every copyright, credit and authorship notice, and
  \ this license.  There is no warranty.

  \ -----------------------------------------------------------
  \ History

  \ 2016-05-01: Start. New words:
  \
  \   color@ color! color-mask@ color-mask! color 2color
  \   permcolor@ permcolor! permcolor-mask@ permcolor-mask!
  \   permcolor 2permcolor paper@ paper! ink@ ink!  bright@
  \   bright! flash! flash@
  \
  \ 2016-05-04: Move `inverse` and `overprint` from the kernel.
  \
  \ 2016-08-01: Move color constants, `papery`, `brighty` and
  \ `flashy` from _Nuclear Invaders_
  \ (http://programandala.net/en.program.nuclear_invaders.html).
  \
  \ 2016-12-02: Fix `bright!`.
  \
  \ 2016-12-03: Rename `>paper` to `paper>attr` and `paper>` to
  \ `attr>paper`, and rewrite them in Z80: much faster, and 2
  \ bytes smaller each.
  \
  \ 2016-12-16: Make `color@`, `color!`, `color-mask@`,
  \ `color-mask!`, `color` and `2color` individually accesible
  \ to `need`.

  \ -----------------------------------------------------------
  \ Documentation

  \ (From the ZX Spectrum +3 manual transcribed by Russell
  \ Marks et al.; and from the ZX Spectrum ROM disassembly.)

  \ System variables:

  \ 23693 = ATTR_P -- permanent colors

  \         {fl}{br}{   paper   }{  ink    }
  \          ___ ___ ___ ___ ___ ___ ___ ___
  \ ATTR_P  |   |   |   |   |   |   |   |   |
  \         |   |   |   |   |   |   |   |   |
  \ 23693   |___|___|___|___|___|___|___|___|
  \           7   6   5   4   3   2   1   0

  \ 23694 = MASK_P -- permanent mask
  \ MASK_P is used for transparent colours. Any bit that is 1
  \ shows that the corresponding attribute is taken not from
  \ ATTR_P but from what is already on the screen.

  \         {fl}{br}{   paper   }{  ink    }
  \          ___ ___ ___ ___ ___ ___ ___ ___
  \ MASK_P  |   |   |   |   |   |   |   |   |
  \         |   |   |   |   |   |   |   |   |
  \ 23694   |___|___|___|___|___|___|___|___|
  \           7   6   5   4   3   2   1   0

  \ 23695 = ATTR_T -- temporary colors

  \         {fl}{br}{   paper   }{  ink    }
  \          ___ ___ ___ ___ ___ ___ ___ ___
  \ ATTR_T  |   |   |   |   |   |   |   |   |
  \         |   |   |   |   |   |   |   |   |
  \ 23695   |___|___|___|___|___|___|___|___|
  \           7   6   5   4   3   2   1   0

  \ 23696 = MASK_T -- temporary mask
  \ MASK_T is used for transparent colours. Any bit that is 1
  \ shows that the corresponding attribute is taken not from
  \ ATTR_T but from what is already on the screen.

  \         {fl}{br}{   paper   }{  ink    }
  \          ___ ___ ___ ___ ___ ___ ___ ___
  \ MASK_T  |   |   |   |   |   |   |   |   |
  \         |   |   |   |   |   |   |   |   |
  \ 23696   |___|___|___|___|___|___|___|___|
  \           7   6   5   4   3   2   1   0

  \ P_FLAG holds the print flags.  Even bits are the temporary
  \ flags; odd bits are the permanent flags.

  \         {paper9 }{ ink9 }{ inv1 }{ over1}
  \          ___ ___ ___ ___ ___ ___ ___ ___
  \ P_FLAG  |   |   |   |   |   |   |   |   |
  \         | p | t | p | t | p | t | p | t |
  \ 23697   |___|___|___|___|___|___|___|___|
  \           7   6   5   4   3   2   1   0

( black blue red magenta green cyan yellow white )

0 cconstant black    1 cconstant blue   2 cconstant red
3 cconstant magenta  4 cconstant green  5 cconstant cyan
6 cconstant yellow   7 cconstant white

( papery brighty flashy )

[unneeded] papery
?\ : papery   ( color -- paper-attribute )           8 *  ;

[unneeded] brighty
?\ : brighty  ( attribute -- brighty-attribute )   64 or  ;

[unneeded] flashy
?\ : flashy   ( attribute -- flashy-attribute )   128 or  ;

( color@ color! color-mask@ color-mask! color 2color )

[unneeded] color@
?\ need os-attr-t  : color@  ( -- b )  os-attr-t c@  ;  exit

  \ doc{
  \
  \ color@  ( -- b )
  \
  \ Get temporary color attribute _b_.
  \
  \ }doc

[unneeded] color!
?\ need os-attr-t  : color!  ( b -- )  os-attr-t c!  ;  exit

  \ doc{
  \
  \ color!  ( b -- )
  \
  \ Set temporary color attribute _b_.
  \
  \ }doc

[unneeded] color-mask@ dup ?\ need os-mask-t
?\ : color-mask@  ( -- b )  os-mask-t c@  ;  exit

  \ doc{
  \
  \ color-mask!  ( b -- )
  \
  \ Get temporary color attribute mask _b_.
  \
  \ }doc

[unneeded] color-mask! dup ?\ need os-mask-t
?\ : color-mask!  ( b -- )  os-mask-t c!  ;  exit

  \ doc{
  \
  \ color-mask!  ( b -- )
  \
  \ Set temporary color attribute mask _b_.
  \
  \ }doc

need ?(  [unneeded] color ?(  need color!
: color  ( b "name" -- )
  create c,  does>  ( -- ) ( pfa ) c@ color!  ;  exit ?)

  \ doc{
  \
  \ color  ( b "name" -- )
  \
  \ Create a definition "name" that, when executed, will
  \ set temporary color attribute _b_.
  \
  \ }doc

need ?(  [unneeded] 2color ?(  need color!  need color-mask!
: 2color  ( b1 b2 "name" -- )
  create 2,  does>  ( -- ) ( pfa ) 2@ color! color-mask!  ; ?)

  \ doc{
  \
  \ 2color  ( b1 b2 "name" -- )
  \
  \ Create a definition "name" that, when executed, will
  \ set temporary color attribute _b2_ and mask _b1_.
  \
  \ }doc

( permcolor@ permcolor! permcolor-mask@ permcolor-mask! )

need os-attr-p  need os-mask-p

: permcolor@  ( -- b )  os-attr-p c@  ;

  \ doc{
  \
  \ permcolor!  ( -- b )
  \
  \ Get permanent color attribute _b_.
  \
  \ }doc

: permcolor!  ( b -- )  os-attr-p c!  ;

  \ doc{
  \
  \ permcolor!  ( b -- )
  \
  \ Set permanent color attribute _b_.
  \
  \ }doc

: permcolor-mask@  ( -- b )  os-mask-p c@  ;

  \ doc{
  \
  \ permcolor-mask@  ( -- b )
  \
  \ Get permanent color attribute mask _b_.
  \
  \ }doc

: permcolor-mask!  ( b -- )  os-mask-p c!  ;

  \ doc{
  \
  \ permcolor-mask!  ( b -- )
  \
  \ Set permanent color attribute mask _b_.
  \
  \ }doc

( permcolor 2permcolor )

need permcolor!  need permcolor-mask!

: permcolor  ( b "name" -- )
  create ,
  does>  ( -- ) ( pfa ) @ permcolor!  ;

  \ doc{
  \
  \ permcolor  ( b "name" -- )
  \
  \ Create a definition "name" that, when executed, will
  \ set permanent color attribute _b_.
  \
  \ }doc

: 2permcolor  ( b1 b2 "name" -- )
  create 2,
  does>  ( -- ) ( pfa ) 2@ permcolor! permcolor-mask!  ;

  \ doc{
  \
  \ 2permcolor  ( b1 b2 "name" -- )
  \
  \ Create a definition "name" that, when executed, will
  \ set permanent color attribute _b2_ and mask _b1_.
  \
  \ }doc

( attr>paper paper>attr paper@ paper! ink@ ink! )

need color@  need color!

code attr>paper  ( n1 -- n2 )
  E1 c, 7D c, E6 c, %00111000 c,
  \ pop hl
  \ ld a,l
  \ and %00111000
  CB c, 3F c, CB c, 3F c, CB c, 3F c, pusha jp,  end-code
  \ srl a
  \ srl a
  \ srl a
  \ jp push_a

  \ doc{
  \
  \ attr>paper  ( n1 -- n2 )
  \
  \ Convert paper attribute _n1_ to actual paper color number
  \ _n2_.
  \
  \ This word is written in Z80. The equivalent code in Forth
  \ is the following:
  \
  \ ----
  \ : attr>paper  ( n1 -- n2 )  %00111000 and 3 rshift  ;
  \ ----
  \
  \ }doc

code paper>attr  ( n1 -- n2 )
  E1 c, 7F c, E6 c, %00000111 c,
  \ pop hl
  \ ld a,l
  \ and %00000111
  CB c, 27 c, CB c, 27 c, CB c, 27 c, pusha jp,  end-code
  \ sla a
  \ sla a
  \ sla a
  \ jp push_a

  \ doc{
  \
  \ paper>attr  ( n1 -- n2 )
  \
  \ Convert paper color _n1_ (only bits 0..2 are used) to paper
  \ attribute _n2_.
  \
  \ This word is written in Z80. The equivalent code in Forth
  \ is the following:
  \
  \ ----
  \ : >paper>  ( n1 -- n2 )  %00000111 and 3 lshift  ;
  \ ----
  \
  \ }doc

: paper@  ( -- b )  color@ attr>paper  ;

: paper!  ( b -- )
  paper>attr color@ %11000111 and or color!  ;

: ink@  ( -- b )  color@ %00000111 and  ;

: ink!  ( b -- )
  %00000111 and color@ %11111000 and or color!  ;

( bright@ bright! flash! flash@ )

need color@  need color!

: bright@  ( -- f )
  color@ %01000000 and 0=  ;

: bright!  ( f -- )
  %01000000 and color@ %10111111 and or color!  ;

: flash@  ( -- f )
  color@ %10000000 and 0=  ;

: flash!  ( f -- )
  %10000000 and color@ %01111111 and or color!  ;

( inverse overprint )

code inverse  ( f -- )
  E1 c,  78 04 + c,  B0 05 + c,  28 c, 06 c,
    \ pop hl
    \ ld a,h
    \ or l
    \ jr z,inverse.off
  FD c, CB c, 57 c, C6 08 02 * + c,  jpnext
    \ set 2,(iy+sys_p_flag_offset) ; temporary inverse flag
    \ _jp_next
    \ inverse.off:
  FD c, CB c, 57 c, 86 08 02 * + c,  jpnext  end-code
    \ res 2,(iy+sys_p_flag_offset) ; temporary inverse flag
    \ _jp_next

  \ doc{
  \
  \ inverse  ( f -- )
  \
  \ If _f_ is zero, turn the inverse mode off; else turn it on.
  \
  \ }doc

code overprint  ( f -- )
  E1 c,  78 04 + c,  B0 05 + c,  28 c, 06 c,
    \ pop hl
    \ ld a,h
    \ or l
    \ jr z,overprint.off
  FD c, CB c, 57 c, C6 08 00 * + c,  jpnext
    \ set 0,(iy+sys_p_flag_offset) ; temporary overprint flag
    \ _jp_next
    \ overprint.off:
  FD c, CB c, 57 c, 86 08 00 * + c,  jpnext  end-code
    \ res 0,(iy+sys_p_flag_offset) ; temporary overprint flag
    \ _jp_next

  \ doc{
  \
  \ overprint  ( f -- )
  \
  \ If _f_ is zero, turn the overprint mode off; else turn it
  \ on.
  \
  \ }doc

  \ vim: filetype=soloforth
