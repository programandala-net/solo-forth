  \ tool.debug.tilde-tilde.fsb
  \
  \ This file is part of Solo Forth
  \ http://programandala.net/en.program.solo_forth.html

  \ Last modified: 201611251110

  \ -----------------------------------------------------------
  \ Description

  \ The `~~` debugging tool.

  \ -----------------------------------------------------------
  \ Author

  \ Marcos Cruz (programandala.net), 2015, 2016.

  \ -----------------------------------------------------------
  \ License

  \ You may do whatever you want with this work, so long as you
  \ retain every copyright, credit and authorship notice, and
  \ this license.  There is no warranty.

  \ -----------------------------------------------------------
  \ History

  \ 2016-02-18: First version.
  \
  \ 2016-11-14: Document all words. Use `defer!`, which is the
  \ kernel, instead of `is`, which is in the library.
  \
  \ 2016-11-25: Add a pause control with a configurable key to
  \ resume. Update and improve documentation. Convert the
  \ default behaviours of `~~save` and `~~restore` to named
  \ words, in order to make them easier to reuse.

( ~~ )

need :noname  need defer

variable ~~?   ~~? on

  \ doc{
  \
  \ ~~?  ( -- a )
  \
  \ A variable that holds a flag. When the flag is true, the
  \ debugging code compiled by `~~` is executed, else ignored.
  \ Its default value is true.
  \
  \ See: `~~`.
  \
  \ }doc

variable ~~x  ~~x off  variable ~~y  ~~y off

  \ doc{
  \
  \ ~~x  ( -- a )
  \
  \ A variable that holds the column the debugging information
  \ compiled by `~~` will be printed at.  Its default value is
  \ zero.
  \
  \ See: `~~`, `~~y`.
  \
  \ }doc

  \ doc{
  \
  \ ~~y  ( -- a )
  \
  \ A variable that holds the row the debugging information
  \ compiled by `~~` will be printed at.  Its default value is
  \ zero.
  \
  \ See: `~~`, `~~x`.
  \
  \ }doc

variable ~~quit-key  ~~quit-key off

  \ doc{
  \
  \ ~~quit-key  ( -- a )
  \
  \ A variable that holds the key code used to quit at the
  \ debugging points compiled by `~~`. If its value is not
  \ zero, `~~control` will wait for a key press in order to
  \ quit the debugging.  Its default value is zero.
  \
  \ See: `~~resume-key`, `~~control`, `~~`.
  \
  \ }doc

variable ~~resume-key  ~~resume-key off

  \ doc{
  \
  \ ~~resume-key  ( -- a )
  \
  \ A variable that holds the key code used to pause at the
  \ debugging points compiled by `~~`. If its value is not
  \ zero, `~~control` will wait for a key press in order to
  \ resume the debugging.  Its default value is zero.
  \
  \ See: `~~quit-key`, `~~control`, `~~`.
  \
  \ }doc

: ~~show  ( nt line block -- )
  ~~x @ ~~y @ at-xy ." Block " . ." Line " . .name .s  ;

  \ doc{
  \
  \ ~~show  ( -- )
  \
  \ Show the debugging info compiled by `~~` and the current
  \ contents of the data stack.
  \
  \ See: `~~`, `~~x`, `~~y`.
  \
  \ }doc

: ~~control?  ( -- f )  ~~resume-key @ ~~quit-key @ or  ;

  \ doc{
  \
  \ ~~control?  ( -- f )
  \ 
  \ Is there any key to be checked by `~~control`?
  \
  \ See: `~~control`.
  \
  \ }doc

: ~~press?  ( c a -- f )  @ tuck = swap 0<> and ;

  \ doc{
  \
  \ ~~press?  ( c a -- f )
  \
  \ Is the contents of _a_ not zero and equal to _c_?
  \ This is a factor of `~~control` used to check key presses.
  \
  \ See: `~~control`.
  \
  \ }doc

: ~~control  ( -- )
  ~~control? 0= ?exit                         begin  key dup
    ~~quit-key ~~press? if  drop quit  then
  ~~resume-key ~~press? if  exit       then   again   ;

  \ doc{
  \
  \ ~~control  ( -- )
  \ 
  \ Keyboard control used by the debug points compiled by `~~`:
  \ If the contents of `~~quit-key` and `~~resume-key` are zero
  \ do nothing, else wait for a key press in an endless loop:
  \ If the pressed key equals the contents of `~~quit-key`,
  \ then execute `quit`; if the pressed key equals the contents
  \ of `~~resume-key`, then exit.
  \
  \ See: `~~quit-key`, `~~resume-key`, `~~`, `~~control?`,
  \ `~~press?`.
  \
  \ }doc

-->

( ~~ )

2variable ~~xy-backup

  \ doc{
  \
  \ ~~xy-backup  ( -- a )
  \
  \ A double variable that holds cursor coordinates saved and
  \ restored by the default behaviours of `~~save` and
  \ `~~restore`.
  \
  \ See: `~~save`, `~~restore`, `~~`.
  \
  \ }doc

defer ~~save  ( -- )  defer ~~restore  ( -- )

  \ doc{
  \
  \ ~~save  ( -- )
  \
  \ Save system status before executing debugging code.  This
  \ is a deferred word. Its default behaviour is to save the
  \ cursor coordinates.
  \
  \ See: `~~`.
  \
  \ }doc

  \ doc{
  \
  \ ~~restore  ( -- )
  \
  \ Restore system status after executing debugging code.  This
  \ is a deferred word. Its default behaviour is to restore the
  \ cursor coordinates.
  \
  \ See: `~~`.
  \
  \ }doc

: ~~save-xy  ( -- )  xy ~~xy-backup 2!  ;

  \ doc{
  \
  \ ~~save-xy  ( -- )
  \
  \ Save the cursor coordinates.  This is the default
  \ behaviour of `~~save`.
  \
  \ See: `~~save`, `~~restore-xy`, `~~restore`.
  \
  \ }doc

: ~~restore-xy  ( -- )  ~~xy-backup 2@ at-xy  ;

  \ doc{
  \
  \ ~~restore-xy  ( -- )
  \
  \ Restore the cursor coordinates.  This is the default
  \ behaviour of `~~restore`.
  \
  \ See: `~~restore`, `~~save-xy`, `~~save`.
  \
  \ }doc

' ~~save-xy ' ~~save defer!  ' ~~restore-xy ' ~~restore defer!

: (~~)  ( nt line block -- )
  ~~? @ if    ~~save ~~show ~~control ~~restore
        else  2drop drop  then  ;

  \ doc{
  \
  \ (~~)  ( nt line block -- )
  \
  \ If the content of `~~?` is not zero, execute the debugging
  \ code that was compiled during the definition of word _nt_
  \ in line _line_ of block _block_.
  \
  \ See: `~~`, `~~?`.
  \
  \ }doc

: ~~  ( -- )
  latest      postpone literal  ( nt )
  >in @ c/l / postpone literal  ( nt line )
  blk @       postpone literal  ( nt line block )
  postpone (~~)  ; immediate compile-only

  \ doc{
  \
  \ ~~  ( -- )
  \
  \ Compile debugging code.
  \
  \ See: `(~~)`, `~~?`.
  \
  \ Origin: Gforth.
  \
  \ }doc

  \ vim: filetype=soloforth
